###########################################################################################
# UNIFIED LETTERFORMS : GREEK ORIGINALS
###########################################################################################
symbol-block 'alpha' : create-glyph 'alpha' : glyph-construction
	set-width WIDTH
	assign-unicode 0x3B1
	include eMarks
	
	local middle : mix SB RIGHTSB 0.42
	local fine   : adviceBlackness 3.25
	local k1 0.25
	local k2 0.5
	local k3 0.35
	include : dispiro
		widths.rhs fine
		g4   (RIGHTSB - OX * 1.75) XH [heading DOWNWARD]
		bezcontrols k1 k2 k3 1 6
		g4.left.mid (middle + CORRECTION_OMIDS) O [widths 0 STROKE]
		archv
		flat.ai (SB + OX * 2) SMALLSMOOTHB
		curl.ai (SB + OX * 2) (XH - SMALLSMOOTHA)
		arcvh
		g4.right.mid (middle - CORRECTION_OMIDS) XO [widths 0 STROKE]
		bezcontrols (1 - k3) 0 (1 - k1) (1 - k2) 6
		g4   (RIGHTSB - OX * 2) 0 [widths.heading 0 fine DOWNWARD]

symbol-block 'Gamma'
	define [GammaShape top] : glyph-construction
		include : VBarLeft (SB * 1.5) 0 top
		include : HBarTop (SB * 1.5 - O) (RIGHTSB - OX) top
		if SLAB : begin
			include : LeftwardTopSerif (SB * 1.5) top SIDEJUT
			include : CenterBottomSerif (SB * 1.5 + HVCONTRAST * HALFSTROKE) 0 JUT
			include : tagged 'serifRT' : DownwardRightSerif (RIGHTSB - OX) top VJUT
	
	create-glyph 'Gamma' : glyph-construction
		assign-unicode 0x393
		include capitalMarks
		include : GammaShape CAP
	alias 'cyrGhe' 0x413 'Gamma'

	create-glyph 'cyrghe.upright' : glyph-construction
		include eMarks
		include : GammaShape XH
	alias 'cyrghe.italic' null 'revs'
	create-glyph 'cyrghe.serbian' : glyph-construction
		include glyphs.dotlessi AS_BASE
		include glyphs.macronAbove	
	italic-variant 'cyrghe' 0x433

	define [CyrGeShape top] : glyph-construction
		include : GammaShape top
		eject-contour 'serifRT'
		include : VBarRight (RIGHTSB - OX) top (top + ACCENT)
	
	create-glyph 'cyrGe' : glyph-construction
		assign-unicode 0x490
		include capitalMarks
		include : CyrGeShape CAP
	
	create-glyph 'cyrge' : glyph-construction
		assign-unicode 0x491
		include eMarks
		include : CyrGeShape XH
	
	create-glyph 'cyrGhayn' : glyph-construction
		assign-unicode 0x492
		include glyphs.cyrGhe AS_BASE
		include : HOverlayBar (SB * 0.3) [mix (SB + STROKE) (RIGHTSB - STROKE) 0.55] (CAP * (1 - BARPOS))
	
	create-glyph 'cyrghayn.upright' : glyph-construction
		include glyphs.'cyrghe.upright' AS_BASE
		include : HOverlayBar (SB * 0.3) [mix (SB + STROKE) (RIGHTSB - STROKE) 0.55] (XH * (1 - BARPOS))
	
	create-glyph 'cyrghayn.italic' : glyph-construction
		include glyphs.'cyrghe.italic' AS_BASE
		include : FlatSlashShape MIDDLE (XH / 2) (OVERLAYSTROKE / 2) (-0.2) 0.75
	alias 'voicedlaryngenalspirant' 0x1D24 'cyrghayn.italic'
	
	italic-variant 'cyrghayn' 0x493

symbol-block 'gamma'
	create-glyph 'gamma' : glyph-construction
		assign-unicode 0x3B3
		include pMarks
	
		local xmid : [mix SB RIGHTSB 0.46] + HALFSTROKE
		include : dispiro
			g4 (SB + 0.4 * STROKE * HVCONTRAST) (XH - O) [widths.rhs]
			bezcontrols 0.33 0.17 1 0.62 6 important
			flat xmid 0
			curl xmid DESCENDER [heading DOWNWARD]
		
		include : dispiro
			widths.rhs
			flat RIGHTSB XH [heading DOWNWARD]
			curl RIGHTSB (XH * 0.9) [heading DOWNWARD]
			quadcontrols 0 0.3 6
			g4   xmid 0 [widths.rhs : STROKE * 0.9]

define {LambdaShape} : symbol-block 'Lambda'
	define [LambdaShape sw] : glyph-construction
		local TURN (CAP * 0.1)
		local curviness 0.2
		include : dispiro
			widths.rhs sw
			flat SB 0    [heading UPWARD]
			curl SB TURN [heading UPWARD]
			quadcontrols 0 curviness
			g4   (MIDDLE - sw / 2) CAP [widths 0 (sw * 0.8)]
		include : dispiro
			widths.lhs sw
			flat RIGHTSB 0    [heading UPWARD]
			curl RIGHTSB TURN [heading UPWARD]
			quadcontrols 0 curviness
			g4   (MIDDLE + sw / 2) CAP [widths (sw * 0.8) 0]
	
		start-from (MIDDLE - sw / 2) CAP
		line-to (MIDDLE + sw / 2) CAP
		line-to MIDDLE (CAP - sw)
		
		if SLAB : begin
			include : CenterBottomSerif (SB + sw / 2 * HVCONTRAST) 0 JUT
			tag-contour 'serif'
			include : CenterBottomSerif (RIGHTSB - sw / 2 * HVCONTRAST) 0 JUT
			tag-contour 'serif'
	
	create-glyph 'Lambda' : glyph-construction
		assign-unicode 0x39B
		include capitalMarks
		include : LambdaShape STROKE
	alias 'latinLambda' 0x245 'Lambda'
	
	list LambdaShape

symbol-block 'Delta and cyrbe'
	create-glyph 'Delta' : glyph-construction
		assign-unicode 0x394
		include glyphs.Lambda AS_BASE
		include : HBar (SB + HALFSTROKE) (RIGHTSB - HALFSTROKE) HALFSTROKE
		eject-contour 'serif'
	
	symbol-block 'delta' : create-glyph 'delta' : glyph-construction
		assign-unicode 0x3B4
		include bMarks
		
		local xNeck : mix SB RIGHTSB 0.07
		local bar : mix 0 XH 0.96
		local xOTLeft : mix SB RIGHTSB 0.5
		local fine : STROKE * 0.5
		
		local sma : SMALLSMOOTHA * bar / XH
		local smb : SMALLSMOOTHB * bar / XH
		
		include : dispiro
			widths.rhs
			g4   [mix MIDDLE RIGHTSB 0.85] ([mix XH CAP 0.8] - STROKE) [heading LEFTWARD]
			alsothru 0.5 0.7
			g4   MIDDLE (CAPO - STROKE)
			archv 2
			g4   (xNeck + STROKE) [mix (CAPO - STROKE) bar 0.5]
			arcvh 2
			g4   xOTLeft bar [heading RIGHTWARD]
			alsothruthem : list {0.25 0.05} {0.5 0.13}
			flat.ai (RIGHTSB - OX) (bar - smb)
			curl.ai (RIGHTSB - OX) sma
			arcvh
			g4.left.mid (MIDDLE + CORRECTION_OMIDS) O
			archv
			flat.ai (SB + OX) smb
			curl.ai (SB + OX) (bar - sma)
			arcvh
			g4   xOTLeft (bar - (STROKE - fine) / 2) [widths 0 fine]
	alias 'cyrbe.serbian' null 'delta'
	alias 'latindelta' 0x1E9F 'delta'
	turned 'turndelta' 0x18D 'delta' MIDDLE (XH / 2) pMarks
	
	create-glyph 'cyrbe' : glyph-construction
		assign-unicode 0x431
		include bMarks
		include : dispiro
			widths.rhs (STROKE * CTHINB)
			flat (SB + OX + STROKE * (1 - CTHINB) * HVCONTRAST) SMALLSMOOTHB
			curl (SB + OX + STROKE * (1 - CTHINB) * HVCONTRAST) (XH - SMALLSMOOTHA)
			arcvh
			g4 (MIDDLE - CORRECTION_OMIDS) (CAP * 0.7 - O) [widths.rhs STROKE]
			archv
			flat.ai (RIGHTSB - OX) (XH - SMALLSMOOTHB)
			curl.ai (RIGHTSB - OX) SMALLSMOOTHA
			arcvh
			g4 (MIDDLE + CORRECTION_OMIDS) O
			archv
			flat (SB + OX) SMALLSMOOTHB
			curl (SB + OX) (XH - SMALLSMOOTHA)
			alsothruthem [list {0.25 0.79} {0.5 0.87}] important
			g4   (RIGHTSB - HALFSTROKE * HVCONTRAST) CAP
			#end : function : set this.dense true

symbol-block 'epsilon and cyrze'
	define [SmallEpsilonShape top hook] : glyph-construction
		local midx : mix SB RIGHTSB 0.65
		local midy : top * BARPOS
		local sma : top - [mix (midy + HALFSTROKE) (top - O - STROKE) (SMOOTHB / (SMOOTHA + SMOOTHB))] - TANSLANT * HVCONTRAST * STROKE
		local smb : [mix (STROKE + O) (midy - HALFSTROKE) (SMOOTHB / (SMOOTHA + SMOOTHB))] + TANSLANT * HVCONTRAST * STROKE
		local fine : STROKE * CTHIN
		include : dispiro
			widths.lhs
			g4 RIGHTSB (top - [fallback hook SHOOK])
			hookstart (top - O)
			g4 (SB + OXE) (top - sma)
			arcvh
			flat MIDDLE (midy - (fine - HALFSTROKE)) [widths.heading fine 0 RIGHTWARD]
			curl midx (midy - (fine - HALFSTROKE)) [heading RIGHTWARD]
		include : dispiro
			widths.rhs
			g4 RIGHTSB [fallback hook SHOOK]
			hookstart O
			g4 (SB + OXE) smb
			arcvh
			flat MIDDLE (midy + (fine - HALFSTROKE)) [widths.heading 0 fine RIGHTWARD]
			curl midx (midy + (fine - HALFSTROKE)) [heading RIGHTWARD]
	
	create-glyph 'epsilon' : glyph-construction
		assign-unicode 0x3B5
		include eMarks
		include : SmallEpsilonShape XH
	alias 'latinepsilon' 0x25B 'epsilon'
	create-glyph 'latinEpsilon' : glyph-construction
		assign-unicode 0x190
		include capitalMarks
		include : SmallEpsilonShape CAP
		
	define [CyrZeShape top hook] : glyph-construction
		local midx : mix RIGHTSB SB 0.65
		local midy : top * BARPOS
		local smb : top - [mix (midy + HALFSTROKE) (top - O - STROKE) (SMOOTHA / (SMOOTHA + SMOOTHB))] + TANSLANT * HVCONTRAST * STROKE
		local sma : [mix (STROKE + O) (midy - HALFSTROKE) (SMOOTHA / (SMOOTHA + SMOOTHB))] - TANSLANT * HVCONTRAST * STROKE
		local fine : STROKE * CTHIN
		include : dispiro
			widths.rhs
			g4 SB (top - [fallback hook SHOOK])
			hookstart (top - O)
			g4 (RIGHTSB - OXE) (top - smb)
			arcvh
			flat MIDDLE (midy - (fine - HALFSTROKE)) [widths.heading 0 fine LEFTWARD]
			curl midx (midy - (fine - HALFSTROKE)) [heading LEFTWARD]
		include : dispiro
			widths.lhs
			g4 SB [fallback hook SHOOK]
			hookstart O
			g4 (RIGHTSB - OXE) sma
			arcvh
			flat MIDDLE (midy + (fine - HALFSTROKE)) [widths.heading fine 0 LEFTWARD]
			curl midx (midy + (fine - HALFSTROKE)) [heading LEFTWARD]
	
	create-glyph 'cyrZe' : glyph-construction
		assign-unicode 0x417
		include capitalMarks
		include : CyrZeShape CAP HOOK
	
	create-glyph 'cyrze' : glyph-construction
		assign-unicode 0x437
		include eMarks
		include : CyrZeShape XH
	alias 'revlatinepsilon' 0x25C 'cyrze'
	turned 'closeepsilon' 0x29A 'closeturnepsilon' MIDDLE (XH / 2)

symbol-block 'Theta'
	create-glyph 'Theta' : glyph-construction
		assign-unicode 0x398
		include capitalMarks
		include glyphs.O AS_BASE
		include : HBar (SB + STROKE * HVCONTRAST - O * 3) (RIGHTSB - STROKE * HVCONTRAST + O * 3) CAPMIDDLE
	create-glyph 'theta' : glyph-construction
		assign-unicode 0x3B8
		include bMarks
		include : OShape CAP 0 (SB - O) (RIGHTSB + O) STROKE (SMALLSMOOTHA * 100) (SMALLSMOOTHB * 100)
		include : HBar (SB + HALFSTROKE) (RIGHTSB - HALFSTROKE) CAPMIDDLE

symbol-block 'zeta and xi'
	create-glyph 'zeta' : glyph-construction
		assign-unicode 0x3B6
		include ifMarks
		include : HBar SB (RIGHTSB + O) (CAP - HALFSTROKE)
		include : dispiro
			widths.rhs
			g4 (RIGHTSB + O) (CAP - STROKE)
			bezcontrols 0.7 0.35 1 0.64 12
			g4.down.mid (SB + STROKE * HVCONTRAST) [mix 0 CAP (0.54 * SMALLSMOOTHB / (SMALLSMOOTHA + SMALLSMOOTHB))]
			arcvh
			g4 [mix SB RIGHTSB 0.55] STROKE [heading RIGHTWARD]
			archv 12
			g4.down.mid RIGHTSB [mix DESCENDER STROKE 0.5] [heading DOWNWARD]
			arcvh 12
			flat (RIGHTSB - (STROKE - [mix DESCENDER STROKE 0.5]) * 1.1) DESCENDER [heading LEFTWARD]
			curl [Math.min (RIGHTSB - (STROKE - [mix DESCENDER STROKE 0.5]) * 1.1 - 1) [mix SB RIGHTSB 0.5]] DESCENDER [heading LEFTWARD]
	
	create-glyph 'xi' : glyph-construction
		assign-unicode 0x3BE
		include ifMarks
		include : HBar SB (RIGHTSB + O) (CAP - HALFSTROKE)
		
		local xbar : mix SB RIGHTSB 0.85
		local ybar : mix 0 CAP 0.55
		include : dispiro
			widths.rhs
			g4.left.start xbar (CAP - STROKE)
			archv
			g4 (SB - O * 2 + STROKE * HVCONTRAST) [mix (CAP - STROKE) ybar 0.6]
			arcvh
			g4.right.end (xbar - HALFSTROKE * TANSLANT) ybar [widths.rhs (STROKE * [mix CTHIN 1 0.5])]
		include : dispiro
			widths.rhs (STROKE * [mix CTHIN 1 0.5])
			g4.left.start (xbar + HALFSTROKE * TANSLANT) (ybar - STROKE)
			bezcontrols 0.7 0 1 0.6
			g4 (SB + STROKE * HVCONTRAST) [mix 0 ybar 0.45] [widths.rhs]
			arcvh
			g4 [mix SB RIGHTSB 0.5] STROKE
			archv
			g4.down.mid RIGHTSB [mix DESCENDER STROKE 0.5] [heading DOWNWARD]
			arcvh
			flat (RIGHTSB - (STROKE - [mix DESCENDER STROKE 0.5]) * 1.1) DESCENDER [heading LEFTWARD]
			curl [Math.min (RIGHTSB - (STROKE - [mix DESCENDER STROKE 0.5]) * 1.1 - 1) [mix SB RIGHTSB 0.5]] DESCENDER [heading LEFTWARD]

symbol-block 'mu' : create-glyph 'mu' : glyph-construction
	set-width WIDTH
	assign-unicode 0x3BC
	include pMarks
	include glyphs.u
	include : dispiro
		widths.rhs
		flat SB DESCENDER [heading UPWARD]
		curl SB (DESCENDER / 2) [heading UPWARD]
		straight.up.end SB SMALLSMOOTHB [widths.heading 0 [adviceBlackness 4] UPWARD]
	if SLAB : begin
		include : LeftwardTopSerif SB XH SIDEJUT

symbol-block 'Xi' : create-glyph 'Xi' : glyph-construction
	assign-unicode 0x39E
	include capitalMarks
	include : HBar [mix SB RIGHTSB 0.125] [mix RIGHTSB SB 0.125] [mix 0 CAP 0.54]
	include : HBar (SB + OX) (RIGHTSB - OX) (CAP - HALFSTROKE)
	include : HBar (SB + OX) (RIGHTSB - OX) (0 + HALFSTROKE)
	if SLAB : begin
		include : DownwardLeftSerif (SB + OX) CAP VJUT
		include : DownwardRightSerif (RIGHTSB - OX) CAP VJUT
		include : UpwardLeftSerif (SB + OX) 0 VJUT
		include : UpwardRightSerif (RIGHTSB - OX) 0 VJUT

define {PiShape} : symbol-block 'Pi'
	define [PiShape top bottom _shrink _fine] : glyph-construction
		local fine : fallback _fine STROKE
		local shrink : if SLAB 0 ([fallback _shrink 0.03] * (RIGHTSB - SB))
		include : HBarTop SB RIGHTSB top fine
		include : VBarLeft (SB + shrink) bottom (top - fine / 2) fine
		include : VBarRight (RIGHTSB - shrink) bottom (top - fine / 2) fine
		
		if SLAB : begin
			include : CenterBottomSerif (SB + shrink + fine * 0.5 * HVCONTRAST) bottom JUT fine
			include : CenterBottomSerif (RIGHTSB - shrink - fine * 0.5 * HVCONTRAST) bottom JUT fine
			include : CenterTopSerif (SB + shrink + fine * 0.5 * HVCONTRAST) top JUT fine
			include : CenterTopSerif (RIGHTSB - shrink - fine * 0.5 * HVCONTRAST) top JUT fine
	
	create-glyph 'Pi' : glyph-construction
		assign-unicode 0x3A0
		include capitalMarks
		include : PiShape CAP 0
	alias 'cyrPe' 0x41F 'Pi'
	
	create-glyph 'cyrpe.upright' : glyph-construction
		include eMarks
		include : PiShape XH 0
	create-glyph 'cyrpe.serbian' : glyph-construction
		include glyphs.u AS_BASE
		include glyphs.macronAbove
	italic-variant 'cyrpe' 0x43f
	
	list PiShape

symbol-block 'pi'
	create-glyph 'pi' : glyph-construction
		assign-unicode 0x3C0
		include eMarks
		include : HBarTop SB RIGHTSB XH
		local m : linreg 72 0.275 108 0.29 STROKE
		local m1 : mix SB RIGHTSB m
		include : dispiro
			widths.rhs
			straight.down.start (m1 + HALFSTROKE * HVCONTRAST) XH [heading DOWNWARD]
			alsothru 0.5 0.85 important
			g4 [mix SB RIGHTSB 0.1] O
		local m2 : mix SB RIGHTSB (1 - m)
		local hook : Math.max (STROKE * HVCONTRAST * 1.1) (HOOK * 0.8)
		include : dispiro
			widths.center
			flat m2 XH [heading DOWNWARD]
			curl m2 hook
			arcvh
			flat [Math.min (RIGHTSB - STROKE * 0.25) (m2 + hook - HALFSTROKE)] HALFSTROKE [heading RIGHTWARD]
			curl RIGHTSB HALFSTROKE [heading RIGHTWARD]

define {SigmaShape} : symbol-block 'Sigma'
	define [SigmaShape top bottom _fine] : glyph-construction
		local cor 1.15
		local fine : fallback _fine STROKE
		
		include : HBarTop SB RIGHTSB top fine
		include : HBarBottom SB RIGHTSB bottom fine
		
		local midx : mix SB RIGHTSB 0.4
		start-from SB (bottom + fine)
		line-to (SB + fine * cor) (bottom + fine)
		line-to (midx + fine * cor) [mix bottom top 0.5]
		line-to (SB + fine * cor) (top - fine)
		line-to SB (top - fine)
		line-to midx [mix bottom top 0.5]
		reverse-last
		
		if SLAB : begin
			include : DownwardRightSerif RIGHTSB top VJUT
			include : UpwardRightSerif RIGHTSB bottom VJUT
	
	create-glyph 'Sigma' : glyph-construction
		set-width WIDTH
		assign-unicode 0x3A3
		include capitalMarks
	
		include : SigmaShape CAP 0
	alias 'lstinSigma' 0x1A9 'Sigma'
	
	list SigmaShape

symbol-block 'Phi, phi and varphi'
	create-glyph 'Phi' : glyph-construction
		assign-unicode 0x3A6
		include capitalMarks
		local y1 : CAP * [if SLAB 0.175 0.125]
		local y2 : CAP * [if SLAB 0.825 0.875]
		include : VBar MIDDLE 0 (y1 + HALFSTROKE)
		include : OShape y2 y1 SB RIGHTSB MVERTSTROKE SMOOTHA SMOOTHB
		include : VBar MIDDLE y1 y2 MVERTSTROKE
		include : VBar MIDDLE (y2 - HALFSTROKE) CAP
		
		if SLAB : begin
			include : tagged 'serifMT' : CenterTopSerif MIDDLE CAP JUT
			include : tagged 'serifMB' : CenterBottomSerif MIDDLE 0 JUT
	alias 'cyrEf' 0x424 'Phi'
	
	create-glyph 'taillessphi' : glyph-construction
		assign-unicode 0x2C77
		include eMarks
		local x0 : mix SB RIGHTSB 0.1
		local y1 : mix 0 XH 0.75
		local x1 : SB + OX * 2
		local y3 : XH * 0.66
		local y4 : XH * 0.65
		include : dispiro
			widths.lhs MVERTSTROKE
			g4 x0 XH
			g4 x1 (XH * 0.55)
			arcvh
			g4 (MIDDLE + CORRECTION_OMIDS) O
			archv
			g4 (WIDTH - x1) (XH * 0.55)
			arcvh 8
			g4.left.mid [mix (WIDTH - x1) (MIDDLE - MVERTSTROKE / 2 * HVCONTRAST) 0.525] XH [heading LEFTWARD]
			archv
			flat (MIDDLE - MVERTSTROKE / 2 * HVCONTRAST) y3
			curl (MIDDLE - MVERTSTROKE / 2 * HVCONTRAST) (MVERTSTROKE * 0.2) [heading DOWNWARD]
	
	create-glyph 'phi' : glyph-construction
		assign-unicode 0x3C6
		include pMarks
		include glyphs.taillessphi
		include : VBar MIDDLE DESCENDER (MVERTSTROKE * 0.2)
	
	create-glyph 'varphi' : glyph-construction
		assign-unicode 0x3D5
		include ifMarks
		local y1 0
		local y2 XH
		include : VBar MIDDLE DESCENDER (y1 + HALFSTROKE)
		include : OShape y2 y1 SB RIGHTSB MVERTSTROKE nothing nothing nothing true
		include : VBar MIDDLE y1 y2 MVERTSTROKE
		include : VBar MIDDLE (y2 - HALFSTROKE) CAP
	
	create-glyph 'cyref' : glyph-construction
		assign-unicode 0x444
		include glyphs.varphi AS_BASE
		if SLAB : begin
			include : tagged 'serifMT' : CenterTopSerif MIDDLE CAP JUT
			include : tagged 'serifMB' : CenterBottomSerif MIDDLE DESCENDER JUT
	alias 'latinphi' 0x278 'cyref'

symbol-block 'Psi'
	create-glyph 'Psi' : glyph-construction
		assign-unicode 0x3A8
		include capitalMarks
		local y : CAP * 0.2
		local y2 : CAP * 0.8
		include : dispiro
			widths.lhs MVERTSTROKE
			flat SB y2 [heading DOWNWARD]
			curl SB (y + SMOOTHB)
			arcvh
			g4   MIDDLE y [heading RIGHTWARD]
			archv
			flat RIGHTSB (y + SMOOTHA)
			curl RIGHTSB y2 [heading UPWARD]
		include : VBar MIDDLE y CAP MVERTSTROKE
		include : VBar MIDDLE 0 (y + HALFSTROKE)
		if SLAB : begin
			include : tagged 'serifLT' : LeftwardTopSerif SB y2 SIDEJUT
			include : tagged 'serifRT' : RightwardTopSerif RIGHTSB y2 SIDEJUT
			include : tagged 'serifMT' : CenterTopSerif MIDDLE CAP JUT
			include : tagged 'serifMB' : CenterBottomSerif MIDDLE 0 JUT
	
	create-glyph 'smcpPsi' : glyph-construction
		assign-unicode 0x1D2A
		include eMarks
		local y : XH * 0.3
		local y2 XH
		include : dispiro
			widths.lhs MVERTSTROKE
			flat SB y2 [heading DOWNWARD]
			curl SB (y + SMOOTHB)
			arcvh
			g4   MIDDLE y [heading RIGHTWARD]
			archv
			flat RIGHTSB (y + SMOOTHA)
			curl RIGHTSB y2 [heading UPWARD]
		include : VBar MIDDLE y XH MVERTSTROKE
		include : VBar MIDDLE 0 (y + HALFSTROKE)
		if SLAB : begin
			include : tagged 'serifLT' : LeftwardTopSerif SB y2 SIDEJUT
			include : tagged 'serifRT' : RightwardTopSerif RIGHTSB y2 SIDEJUT
			include : tagged 'serifMB' : CenterBottomSerif MIDDLE 0 JUT
	
	create-glyph 'psi' : glyph-construction
		assign-unicode 0x3C8
		include ifMarks
		local fine : adviceBlackness 3.5
		include : dispiro
			widths.lhs fine
			flat SB XH [heading DOWNWARD]
			curl SB SMALLSMOOTHB
			arcvh
			g4   MIDDLE O [heading RIGHTWARD]
			archv
			flat RIGHTSB SMALLSMOOTHA
			curl RIGHTSB XH [heading UPWARD]
		include : VBar MIDDLE 0 CAP fine
		include : VBar MIDDLE DESCENDER HALFSTROKE
		if SLAB : begin
			include : LeftwardTopSerif SB XH SIDEJUT
			tag-contour 'serifLT'
			if (!para.isItalic) : begin
				include : tagged 'serifRT' : RightwardTopSerif RIGHTSB XH SIDEJUT

symbol-block 'Omega'
	define [OmegaShape top extend sma smb] : glyph-construction
		local fine : STROKE * CTHIN
		local x1 [mix SB RIGHTSB 0.4]
		local x2 (WIDTH - x1)
		local yattach : top * extend + STROKE
		include : dispiro
			g4   x1 (yattach - fine) [widths.heading 0 fine LEFTWARD]
			archv
			flat SB (yattach - STROKE + smb) [widths 0 STROKE]
			curl SB (top - sma)
			arcvh
			g4   (MIDDLE - CORRECTION_OMIDS) (top - O)
			archv
			flat RIGHTSB (top - smb)
			curl RIGHTSB (yattach - STROKE + sma)
			arcvh
			g4   x2 (yattach - fine) [widths.heading 0 fine LEFTWARD]
		include : VBarRight x1 0 (yattach) fine
		include : HBar SB x1 HALFSTROKE
		include : VBarLeft x2 0 (yattach) fine
		include : HBar x2 RIGHTSB HALFSTROKE
	
	create-glyph 'Omega' : glyph-construction
		assign-unicode 0x3A9
		include capitalMarks
		include : OmegaShape CAP 0.08 SMOOTHA SMOOTHB
	
	turned 'latinUpsilon1' 0x1B1 'Omega' MIDDLE (CAP / 2)
	
	create-glyph 'latinupsilon1' : glyph-construction
		assign-unicode 0x28A
		include eMarks
		include : OmegaShape XH 0.08 SMALLSMOOTHA SMALLSMOOTHB
		include : FlipAround MIDDLE (XH / 2)

symbol-block 'omega'
	create-glyph 'omega' : glyph-construction
		assign-unicode 0x3C9
		include eMarks
		local fine : adviceBlackness 3.25
		local mfine : fine * CTHIN
		local x0 : mix SB RIGHTSB 0.1
		local y0 : XH - STROKE * 0.2
		local y1 : mix 0 XH 0.8
		local x1 : SB + OX * 2
		local y3 : XH / 2
		local y4 : XH * 0.65
		
		include : dispiro
			widths.lhs fine
			g4 x0 y0
			g4 x1 (XH / 2)
			arcvh 8
			g4 [mix x1 (MIDDLE + fine / 2 * HVCONTRAST) 0.5] O [heading RIGHTWARD]
			archv 8
			flat (MIDDLE + (mfine - fine / 2) * HVCONTRAST) y3 [widths.heading mfine 0 UPWARD]
			curl (MIDDLE + (mfine - fine / 2) * HVCONTRAST) y4 [heading UPWARD]
			end [function : set this.angles 4]
		include : dispiro
			widths.rhs fine
			g4 (WIDTH - x0) y0
			g4 (WIDTH - x1) (XH / 2)
			arcvh 8
			g4 [mix (WIDTH - x1) (MIDDLE - fine / 2 * HVCONTRAST) 0.5] O [heading LEFTWARD]
			archv 8
			flat (MIDDLE - (mfine - fine / 2) * HVCONTRAST) y3 [widths.heading 0 mfine UPWARD]
			curl (MIDDLE - (mfine - fine / 2) * HVCONTRAST) y4 [heading UPWARD]
			end [function : set this.angles 4]
	
	create-glyph 'pomega' : glyph-construction
		assign-unicode 0x3D6
		include glyphs.omega AS_BASE
		include : HBar SB RIGHTSB (XH - HALFSTROKE)
	
	create-glyph 'closeomega' : glyph-construction
		assign-unicode 0x277
		include eMarks
		local fine : adviceBlackness 3.25
		local mfine : fine * CTHIN
		local x0 : MIDDLE - CORRECTION_OMIDS
		local y0 : XH - O
		local y1 : mix 0 XH 0.45
		local x1 : SB + OX * 2
		local y3 : XH / 2
		local y4 : XH * 0.65
		include : dispiro
			widths.rhs mfine
			flat (MIDDLE + (mfine - fine / 2) * HVCONTRAST) y4 [heading DOWNWARD]
			curl (MIDDLE + (mfine - fine / 2) * HVCONTRAST) y3 [heading DOWNWARD]
			arcvh 8
			g4 [mix x1 (MIDDLE + fine / 2 * HVCONTRAST) 0.5] O [widths.heading 0 fine LEFTWARD]
			archv 8
			g4 x1 y1
			arcvh
			g4 x0 y0
			archv
			g4 (WIDTH - x1) y1
			arcvh 8
			g4 [mix (WIDTH - x1) (MIDDLE - fine / 2 * HVCONTRAST) 0.5] O [heading LEFTWARD]
			archv 8
			flat (MIDDLE - (mfine - fine / 2) * HVCONTRAST) y3 [widths.heading 0 mfine UPWARD]
			curl (MIDDLE - (mfine - fine / 2) * HVCONTRAST) y4 [heading UPWARD]
			end [function : set this.angles 4]

###########################################################################################
# UNIFIED LETTERFORMS : CYRILLIC ORIGINALS
###########################################################################################

define {CyrYeriShape RevCyrYeriShape} : symbol-block 'CyrYeri-related'
	define [CyrYeriShape top _left _right _fine _jut] : glyph-construction
		local fine : fallback _fine STROKE
		local left : fallback _left SB
		local right : fallback _right RIGHTSB
		local jut : fallback _jut JUT
		
		local bowl : top * 0.55 + HALFSTROKE
		local turnRadius : bowl * 0.55
		local turnbottom : mix 0 bowl (SMOOTHA / (SMOOTHA + SMOOTHB))
		include : dispiro
			widths.lhs fine
			flat (left + STROKE * 0.2) 0 [heading RIGHTWARD]
			curl ([Math.max (left + fine * HVCONTRAST) (right - turnRadius * [Math.pow ((right - left) / (RIGHTSB - SB)) 0.5])] + CORRECTION_OMIDX * fine) 0
			archv 8
			g4   (right - OX) turnbottom
			arcvh 8
			flat ([Math.max (left + fine * HVCONTRAST) (right - turnRadius * [Math.pow ((right - left) / (RIGHTSB - SB)) 0.5])] - CORRECTION_OMIDX * fine) bowl
			curl (left + STROKE * 0.2) bowl [heading LEFTWARD]
		include : VBarLeft left 0 top fine
		if SLAB : begin
			include : LeftwardBottomSerif left 0 (jut - fine / 2 * HVCONTRAST)
			tag-contour 'serifYeriLB'
			include : CenterTopSerif (left + fine / 2 * HVCONTRAST) top jut
			tag-contour 'serifYeriLT'
	
	define [RevCyrYeriShape top _left _right _fine _jut] : glyph-construction
		local fine : fallback _fine STROKE
		local left : fallback _left SB
		local right : fallback _right RIGHTSB
		local jut : fallback _jut JUT
		
		local bowl : top * 0.55 + HALFSTROKE
	
		local turnRadius : bowl * 0.55
		local turnbottom : mix 0 bowl (SMOOTHB / (SMOOTHA + SMOOTHB))
		include : dispiro
			widths.rhs
			flat (right - STROKE * 0.2) 0 [heading LEFTWARD]
			curl (left + turnRadius * [Math.pow ((right - left) / (RIGHTSB - SB)) 0.5] + CORRECTION_OMIDX * fine) 0
			archv
			g4   (left + O) turnbottom [widths.rhs fine]
			arcvh
			flat (left + turnRadius * [Math.pow ((right - left) / (RIGHTSB - SB)) 0.5] - CORRECTION_OMIDX * fine) bowl [widths.rhs]
			curl (right - STROKE * 0.2) bowl [heading RIGHTWARD]
		include : VBarRight right 0 top fine
		if SLAB : begin
			include : RightwardBottomSerif right 0 (jut - fine / 2 * HVCONTRAST)
			tag-contour 'serifYeriRB'
			include : CenterTopSerif (right - fine / 2 * HVCONTRAST) top jut
			tag-contour 'serifYeriRT'
	
	create-glyph 'cyrYeri' : glyph-construction
		assign-unicode 0x42C
		include capitalMarks
		include : CyrYeriShape CAP SB RIGHTSB
	
	create-glyph 'cyryeri' : glyph-construction
		assign-unicode 0x44C
		include eMarks
		include : CyrYeriShape XH SB RIGHTSB
	
	create-glyph 'cyrBe' : glyph-construction
		assign-unicode 0x411
		include glyphs.cyrYeri AS_BASE
		include : HBar SB [mix SB RIGHTSB 0.9] (CAP - HALFSTROKE)
		if SLAB : begin
			include : DownwardRightSerif [mix SB RIGHTSB 0.9] CAP VJUT
	alias 'latinBe' 0x182 'cyrBe'
	
	create-glyph 'ZhuangToneSix' : glyph-construction
		assign-unicode 0x184
		include glyphs.cyrYeri AS_BASE
		eject-contour 'serifYeriLT'
		local s : Math.max STROKE (XH * 0.1)
		start-from (SB - O) CAP
		line-to    SB CAP
		line-to    (SB - s) (CAP - s)
		line-to    SB (CAP - s * 2)
		line-to    (SB - O) (CAP - s * 2)
		reverse-last
	
	create-glyph 'latinDe' : glyph-construction
		assign-unicode 0x18B
		include capitalMarks
		include : RevCyrYeriShape CAP
		include : HBarTop [mix RIGHTSB SB 0.9] (RIGHTSB + O) CAP
		if SLAB : begin
			include : DownwardLeftSerif [mix RIGHTSB SB 0.9] CAP VJUT
	
	define [CyrYerShape top] : glyph-construction
		local left : if SLAB ([mix SB RIGHTSB 0.35] - MVERTSTROKE / 2 * HVCONTRAST) [mix SB RIGHTSB 0.1]
		local barleft : mix 0 SB [if SLAB 0.25 0.6]
		include : CyrYeriShape top left RIGHTSB
		include : HBarTop barleft (STROKE * 0.1 + left) top
		if SLAB : begin
			include : DownwardLeftSerif barleft top VJUT (MVERTSTROKE / HVCONTRAST)
	
	create-glyph 'cyrYer' : glyph-construction
		assign-unicode 0x42A
		include capitalMarks
		include : CyrYerShape CAP
	
	create-glyph 'cyryer' : glyph-construction
		assign-unicode 0x44A
		include eMarks
		include : CyrYerShape XH
	
	define [CyrYeryShape top] : glyph-construction
		include : CyrYeriShape top (SB + O) [mix (RIGHTSB - MVERTSTROKE * HVCONTRAST) (MIDDLE + MVERTSTROKE / 2 * HVCONTRAST) 0.5] MVERTSTROKE (JUT * 0.75)
		include : VBarRight (RIGHTSB - O) 0 top MVERTSTROKE
		if SLAB : begin
			include : CenterTopSerif (RIGHTSB - O - MVERTSTROKE / 2 * HVCONTRAST) top (JUT * 0.75)
			include : CenterBottomSerif (RIGHTSB - O - MVERTSTROKE / 2 * HVCONTRAST) 0 (JUT * 0.75)
	
	create-glyph 'cyrYery' : glyph-construction
		assign-unicode 0x42B
		include capitalMarks
		include : CyrYeryShape CAP
	
	create-glyph 'cyryery' : glyph-construction
		assign-unicode 0x44B
		include eMarks
		include : CyrYeryShape XH
	
	list CyrYeriShape RevCyrYeriShape

symbol-block 'CyrI'
	define [CyrIShape top] : glyph-construction
		local topstroke : adviceBlackness 4
		local halftopstroke : topstroke / 2
		include : dispiro
			widths.lhs
			flat RIGHTSB 0 [heading UPWARD]
			curl RIGHTSB (top * 0.4) [heading UPWARD]
			straight.up.end RIGHTSB top [widths.heading topstroke 0 UPWARD]
		include : dispiro
			widths.lhs
			flat SB top [heading DOWNWARD]
			curl SB (top * 0.6) [heading DOWNWARD]
			straight.down.end SB 0 [widths.heading topstroke 0 DOWNWARD]
		include : dispiro
			flat (RIGHTSB - halftopstroke) top [widths.heading 0 topstroke DOWNWARD]
			curl (SB + halftopstroke) 0 [widths.heading topstroke 0 DOWNWARD]
		include : AICyrISerifs top
	
	create-glyph 'cyrI' : glyph-construction
		assign-unicode 0x418
		include capitalMarks
		include : CyrIShape CAP
	
	create-glyph 'cyri.upright' : glyph-construction
		include eMarks
		include : CyrIShape XH
	
	create-glyph 'cyri.italic' : glyph-construction
		include glyphs.u AS_BASE
	
	italic-variant 'cyri' 0x438

symbol-block 'CyrTse'
	define [CyrTseShape top] : glyph-construction
		include : VBarLeft SB 0 top
		include : HBarBottom SB RIGHTSB 0
		include : VBarRight RIGHTSB 0 top
		include : CyrDescender RIGHTSB 0.05
		if SLAB : begin
			include : AIVSerifs top
			include : LeftwardBottomSerif SB 0 SIDEJUT
			include : RightwardBottomSerif RIGHTSB 0 SIDEJUT
	
	create-glyph 'cyrTse' : glyph-construction
		assign-unicode 0x426
		include capitalMarks
		include : CyrTseShape CAP
	
	create-glyph 'cyrtse.upright' : glyph-construction
		include eMarks
		include : CyrTseShape XH
	create-glyph 'cyrtse.italic' : glyph-construction
		include eMarks
		include glyphs.u
		eject-contour 'serifLT'
		include : CyrDescender RIGHTSB 0.05
	italic-variant 'cyrtse' 0x446
	
	define [CyrDzheShape top] : glyph-construction
		include : VBarLeft SB 0 top
		include : HBarBottom SB RIGHTSB 0
		include : VBarRight RIGHTSB 0 top
		include : VBar MIDDLE DESCENDER STROKE
		if SLAB : begin
			include : AIVSerifs top
			include : LeftwardBottomSerif SB 0 SIDEJUT
			include : RightwardBottomSerif RIGHTSB 0 SIDEJUT
	
	create-glyph 'cyrDzhe' : glyph-construction
		assign-unicode 0x40F
		include ifMarks
		include : CyrDzheShape CAP
	
	create-glyph 'cyrdzhe.upright' : glyph-construction
		include pMarks
		include : CyrDzheShape XH
	create-glyph 'cyrdzhe.italic' : glyph-construction
		include glyphs.u AS_BASE
		include glyphs.barBelow
	italic-variant 'cyrdzhe' 0x45F
	
symbol-block 'CyrDe'
	define [CyrDeShape top] : glyph-construction
		local descenderOverflow : if SLAB SIDEJUT ((RIGHTSB - SB) * 0.05)
		local cutleft SB
		local cutright : if SLAB RIGHTSB (RIGHTSB - descenderOverflow)
		local topleft : mix cutleft cutright 0.15
		include : HBarBottom (cutleft - descenderOverflow) (cutright + descenderOverflow) 0
		include : VBarRight cutright 0 top
		include : halfXStrand (topleft + STROKE * HVCONTRAST) top (cutleft + HALFSTROKE * HVCONTRAST) HALFSTROKE 0.1 0.75 0.5
		include : VBarLeft (cutleft - descenderOverflow) (-LONGJUT + HALFSTROKE) 0
		include : VBarRight (cutright + descenderOverflow) (-LONGJUT + HALFSTROKE) 0
		
		if SLAB : then
			include : dispiro
				widths.rhs
				flat (topleft - descenderOverflow) top
				curl (cutright + descenderOverflow) top
		: else
			include : HBarTop topleft cutright top
	
	create-glyph 'cyrDe' : glyph-construction
		assign-unicode 0x414
		include ifMarks
		include : CyrDeShape CAP
	
	create-glyph 'cyrde.upright' : glyph-construction
		include eMarks
		include : CyrDeShape XH
	
	create-glyph 'cyrde.italic' : glyph-construction
		include bMarks
		include : dispiro
			widths.lhs (STROKE * CTHINB)
			flat (RIGHTSB - OX - STROKE * HVCONTRAST * (1 - CTHINB)) SMALLSMOOTHA
			curl (RIGHTSB - OX - STROKE * HVCONTRAST * (1 - CTHINB)) (XH - SMALLSMOOTHB)
			arcvh
			g4 (MIDDLE - CORRECTION_OMIDS) (CAP * 0.7 - O) [widths.lhs]
			archv
			flat.ai (SB + OX) (XH - SMALLSMOOTHA)
			curl.ai (SB + OX) SMALLSMOOTHB
			arcvh
			g4 (MIDDLE + CORRECTION_OMIDS) O
			archv
			flat (RIGHTSB - OX) SMALLSMOOTHA
			curl (RIGHTSB - OX) (XH - SMALLSMOOTHB)
			quadcontrols 0 0.8
			g4 (SB + STROKE * 1.1) CAP
	
	alias 'cyrde.serbian' null 'g.singlestorey'
	italic-variant 'cyrde' 0x434

symbol-block 'CyrEl'
	define [CyrElShape top] : glyph-construction
		local cutleft : mix SB RIGHTSB 0.125
		local cutleft2 : mix SB RIGHTSB 0.075
		local cutright : mix SB RIGHTSB 0.95
		include : VBarRight cutright 0 top
		include : HBar cutleft cutright (top - HALFSTROKE)
		include : LegShape cutleft cutleft2 [mix SB 0 [if SLAB 0.5 0.25]] top 0
		if SLAB : begin
			include : RightwardTopSerif cutright top SIDEJUT
			include : LeftwardTopSerif cutleft top SIDEJUT
			include : CenterBottomSerif (cutright - HALFSTROKE * HVCONTRAST) 0 JUT
	
	create-glyph 'cyrEl' : glyph-construction
		assign-unicode 0x41B
		include capitalMarks
		include : CyrElShape CAP
	
	create-glyph 'cyrel' : glyph-construction
		assign-unicode 0x43B
		include eMarks
		include : CyrElShape XH

symbol-block 'CyrZhe'
	define [CyrZheShape top] : glyph-construction
		local fine : adviceBlackness 3.3
		local midx : mix SB MIDDLE 0.3
		define [CyrAngleShape] : glyph-construction
			local cor HVCONTRAST
			start-from (SB + O * 4) 0
			line-to (SB + fine * cor + O * 4) 0
			line-to (midx + fine * cor) (top / 2)
			line-to (SB + fine * cor + O * 4) top
			line-to (SB + O * 4) top
			line-to midx (top / 2)
			reverse-last
		include : CyrAngleShape
		include : FlipAround MIDDLE (top / 2)
		include : CyrAngleShape
		include : VBar MIDDLE 0 top fine
		include : HBar midx (WIDTH - midx) (top / 2) fine
	
	create-glyph 'cyrZhe' : glyph-construction
		assign-unicode 0x416
		include capitalMarks
		include : CyrZheShape CAP
	composite nothing glyphs.cyrZhe [CyrDescender (RIGHTSB - O * 4)] [into-unicode 0x496]
	
	create-glyph 'cyrzhe' : glyph-construction
		assign-unicode 0x436
		include eMarks
		include : CyrZheShape XH
	composite nothing glyphs.cyrzhe [CyrDescender (RIGHTSB - O * 4)] [into-unicode 0x497]

symbol-block 'CyrSha'
	define [CyrShaShape top] : glyph-construction
		include : HBarBottom SB RIGHTSB 0
		include : VBarLeft SB 0 top MVERTSTROKE
		include : VBarRight RIGHTSB 0 top MVERTSTROKE
		include : VBar MIDDLE 0 top MVERTSTROKE
		if SLAB : begin
			include : tagged 'serifLT' : LeftwardTopSerif SB top SIDEJUT
			include : tagged 'serifLB' : LeftwardBottomSerif SB 0 SIDEJUT
			include : tagged 'serifRT' : RightwardTopSerif RIGHTSB top SIDEJUT
			include : tagged 'serifRB' : RightwardBottomSerif RIGHTSB 0 SIDEJUT
	create-glyph 'cyrSha' : glyph-construction
		assign-unicode 0x428
		include capitalMarks
		include : CyrShaShape CAP
	
	create-glyph 'cyrsha.upright' : glyph-construction
		include eMarks
		include : CyrShaShape XH
	turned 'cyrsha.italic' null 'm' MIDDLE (XH / 2)
	italic-variant 'cyrsha' 0x448
	
	define [CyrShchaShape top] : glyph-construction
		include : CyrShaShape top
		#eject-contour 'serifRB'
		include : CyrDescender RIGHTSB 0.05
	
	create-glyph 'cyrShcha' : glyph-construction
		assign-unicode 0x429
		include capitalMarks
		include : CyrShchaShape CAP
	
	create-glyph 'cyrshcha.upright' : glyph-construction
		include eMarks
		include : CyrShchaShape XH
	create-glyph 'cyrshcha.italic' : glyph-construction
		include eMarks
		include glyphs.'cyrsha.italic'
		include : CyrDescender RIGHTSB 0.05
	italic-variant 'cyrshcha' 0x449
	
	# Serbian pe and te
	create-glyph 'cyrte.serbian' : glyph-construction
		include glyphs.'cyrsha.italic' AS_BASE
		include glyphs.macronAbove
	italic-variant 'cyrte' 0x442

symbol-block 'CyrChe'
	define [CyrCheShape top _barp] : glyph-construction
		local bar : top * [fallback _barp 0.5]
		include : VBarRight RIGHTSB 0 top
		include : dispiro
			widths.lhs
			flat SB top [heading DOWNWARD]
			curl SB (bar + SMOOTHB - HALFSTROKE)
			arcvh
			flat MIDDLE (bar - HALFSTROKE)
			curl (RIGHTSB - 1) (bar - HALFSTROKE) [heading RIGHTWARD]
		if SLAB : begin
			include : CenterTopSerif (RIGHTSB - HALFSTROKE * HVCONTRAST) top JUT
			include : CenterTopSerif (SB + HALFSTROKE * HVCONTRAST) top JUT
			include : CenterBottomSerif (RIGHTSB - HALFSTROKE * HVCONTRAST) 0 JUT
	
	create-glyph 'cyrChe' : glyph-construction
		assign-unicode 0x427
		include capitalMarks
		include : CyrCheShape CAP [if SLAB 0.45 0.35]
	composite nothing glyphs.cyrChe [CyrDescender RIGHTSB] [into-unicode 0x4B6]
	
	create-glyph 'cyrche' : glyph-construction
		assign-unicode 0x447
		include eMarks
		include : CyrCheShape XH [if SLAB 0.45 0.4]
	composite nothing glyphs.cyrche [CyrDescender RIGHTSB] [into-unicode 0x4B7]
	
	turned 'cyrShha' 0x4BA 'cyrChe' MIDDLE (CAP / 2)
	
	create-glyph 'cyrChevbar' : glyph-construction
		assign-unicode 0x4B8
		include glyphs.cyrChe AS_BASE
		local yc (CAP * [if SLAB 0.45 0.35] + STROKE * 0.1)
		include : VBar MIDDLE (yc + LONGJUT * 0.8) (yc - LONGJUT * 0.8) OVERLAYSTROKE

	create-glyph 'cyrchevbar' : glyph-construction
		assign-unicode 0x4B9
		include glyphs.cyrche AS_BASE
		local yc (XH * [if SLAB 0.45 0.35] + STROKE * 0.1)
		include : VBar MIDDLE (yc + LONGJUT * 0.8) (yc - LONGJUT * 0.8) OVERLAYSTROKE

symbol-block 'CyrYu'
	define [CyrYuShape top sma smb] : glyph-construction
		local xm : mix (SB + O + MVERTSTROKE * HVCONTRAST) (RIGHTSB - OX) 0.175
		include : VBarLeft (SB + O) 0 top MVERTSTROKE
		include : OShape top 0 xm (RIGHTSB - OX) MVERTSTROKE (sma * 0.8) (smb * 0.8)
		include : HBar (SB + O + 1) xm (top / 2)
		
		if SLAB : begin
			include : CenterTopSerif (SB + O + MVERTSTROKE / 2 * HVCONTRAST) top (JUT * 0.75)
			include : CenterBottomSerif (SB + O + MVERTSTROKE / 2 * HVCONTRAST) 0 (JUT * 0.75)
	
	create-glyph 'cyrYu' : glyph-construction
		assign-unicode 0x42E
		include capitalMarks
		include : CyrYuShape CAP SMOOTHA SMOOTHB
	
	create-glyph 'cyryu' : glyph-construction
		assign-unicode 0x44E
		include eMarks
		include : CyrYuShape XH SMALLSMOOTHA SMALLSMOOTHB

symbol-block 'CyrLje and CyrNje'
	define [CyrLjeShape top] : glyph-construction
		local xlefttop : mix SB RIGHTSB 0.075
		local jut : JUT * 0.72
		include : LegShape xlefttop [mix SB RIGHTSB 0.025] [mix SB 0 [if SLAB 0.75 0.5]] top 0 MVERTSTROKE
		include : CyrYeriShape top (MIDDLE - MVERTSTROKE / 2 * HVCONTRAST) (RIGHTSB - O) MVERTSTROKE jut
		include : HBarTop xlefttop MIDDLE top
		if SLAB : begin
			include : LeftwardTopSerif xlefttop top (jut - MVERTSTROKE / 2 * HVCONTRAST)
	
	create-glyph 'cyrLje' : glyph-construction
		assign-unicode 0x409
		include capitalMarks
		include : CyrLjeShape CAP
	
	create-glyph 'cyrlje' : glyph-construction
		assign-unicode 0x459
		include eMarks
		include : CyrLjeShape XH

	define [CyrNjeShape top] : glyph-construction
		local xlefttop : mix SB RIGHTSB 0.075
		local jut : JUT * 0.72
		include : VBarLeft SB 0 top MVERTSTROKE
		include : CyrYeriShape top (MIDDLE - MVERTSTROKE / 2 * HVCONTRAST) (RIGHTSB - O) MVERTSTROKE jut
		include : HBar (SB + MVERTSTROKE * 0.1) MIDDLE (top / 2)
		if SLAB : begin
			include : CenterTopSerif (SB + MVERTSTROKE / 2 * HVCONTRAST) top jut
			include : CenterBottomSerif (SB + MVERTSTROKE / 2 * HVCONTRAST) 0 jut
	
	create-glyph 'cyrNje' : glyph-construction
		assign-unicode 0x40A
		include capitalMarks
		include : CyrNjeShape CAP
	
	create-glyph 'cyrnje' : glyph-construction
		assign-unicode 0x45A
		include eMarks
		include : CyrNjeShape XH

symbol-block 'CyrTshe and CyrDje'
	create-glyph 'cyrTshe' : glyph-construction
		assign-unicode 0x40B
		include capitalMarks
		
		local left : mix SB RIGHTSB 0.15
		
		include : nShoulder (left + STROKE * HVCONTRAST) RIGHTSB SHOULDERFINE
		include : VBarLeft left 0 CAP
		include : HBarTop SB [mix left RIGHTSB 0.5] CAP
	
	alias 'cyrtshe' 0x45B 'hbar'
	
	create-glyph 'cyrDje' : glyph-construction
		assign-unicode 0x402
		include capitalMarks
		
		local left : mix SB RIGHTSB 0.15
		
		include : nShoulder (left + STROKE * HVCONTRAST) RIGHTSB SHOULDERFINE XH (HOOK + HALFSTROKE + O) SMOOTHA SMOOTHB
		include : VBarLeft left 0 CAP
		include : HBarTop SB [mix left RIGHTSB 0.5] CAP
		include : VerticalHook (RIGHTSB - HALFSTROKE * HVCONTRAST) (HOOK + HALFSTROKE + O) [Math.max ((left - RIGHTSB) / 2 + HALFSTROKE) (-HOOK * 1.2)] HOOK
	
	create-glyph 'cyrdje' : glyph-construction
		assign-unicode 0x452
		include ifMarks
		include glyphs.cyrtshe
		eject-contour 'serifRB'
		include : VerticalHook (RIGHTSB - HALFSTROKE * HVCONTRAST) 0 (-HOOK * 1.2) HOOK

###########################################################################################
# UNIFIED LETTERFORMS : OTHER LATIN
###########################################################################################
symbol-block 'eszet'
	create-glyph 'eszet' : glyph-construction
		set-width WIDTH
		assign-unicode 0xDF
		include ifMarks
		local l : SB * 1
		include : dispiro
			widths.lhs
			g4 ([mix SB RIGHTSB 0.75] + HALFSTROKE * HVCONTRAST) (CAP - HOOK)
			hookstart (CAP - O)
			flat l XH
			curl l 0 [heading DOWNWARD]
		local t : mix 0 CAP 0.7
		local tm : [mix DESCENDER t 0.625] + HALFSTROKE
		local tl : [mix l RIGHTSB 0.35] + HALFSTROKE * HVCONTRAST
		include : HBarTop (l + 1) (RIGHTSB - HALFSTROKE * 1.2 - OX) t
		include : dispiro
			widths.rhs
			flat tl tm [heading RIGHTWARD]
			curl (tl + 1) tm [heading RIGHTWARD]
			g2 (RIGHTSB - OX * 1.5) [mix DESCENDER tm 0.70]
			g2 (RIGHTSB - OX * 1.5) [mix DESCENDER tm 0.67]
			alsothru 0.5 0.75
			g4 [mix SB RIGHTSB 0.35] DESCENDER
		include : dispiro
			widths.center (STROKE * 1.1)
			corner tl (tm - STROKE) [heading UPWARD]
			corner (RIGHTSB - HALFSTROKE * 1.2 - OX) t [heading UPWARD]
		set this.italicHookAttachPoint {.x (l + HALFSTROKE) .y 0}
	
	create-glyph 'Eszet' : glyph-construction
		assign-unicode 0x1E9E
		include capitalMarks
		include : dispiro
			widths.lhs
			g4 (RIGHTSB + O * 2) (CAP - SMOOTHB)
			hookstart (CAP - O)
			flat SB XH
			curl SB 0 [heading DOWNWARD]
		local ymiddle : [mix 0 CAP 0.5] - HALFSTROKE
		include : dispiro
			widths.rhs
			g4   (RIGHTSB - ymiddle / 2 - STROKE * 0.75) (ymiddle + STROKE)
			archv
			g4   (RIGHTSB - O) [mix 0 (ymiddle + STROKE) 0.5]
			arcvh
			flat (RIGHTSB - ymiddle / 2 - HALFSTROKE) 0
			curl (SB + STROKE - O * 3) 0 [heading LEFTWARD]
		include : dispiro
			widths.rhs
			g4 (RIGHTSB + O * 2) (CAP - SMOOTHB)
			g4 (RIGHTSB - ymiddle / 2 - STROKE * 0.75) (ymiddle + STROKE) [widths STROKE 0]

symbol-block 'Digraphs'
	create-glyph 'AE' : glyph-construction
		set-width WIDTH
		assign-unicode 0xC6
		include capitalMarks
		
		local eleft : MIDDLE - MVERTSTROKE * 0.25
		local turn : XH * 0.1
		
		# A half
		include : dispiro
			widths.rhs MVERTSTROKE
			flat SB 0 [heading UPWARD]
			curl SB (CAP * 0.1) [heading UPWARD]
			quadcontrols 0 0.3 6 unimportant
			g4   (eleft - HALFSTROKE) CAP [widths.rhs (MVERTSTROKE * 0.8)]
	
		start-from (eleft - HALFSTROKE) CAP
		line-to    eleft CAP
		line-to    eleft (CAP - MVERTSTROKE)
		include : HBarTop  (SB + MVERTSTROKE)  (eleft + MVERTSTROKE / 2) (XH / 2) MVERTSTROKE
		
		# E half
		include : VBarLeft eleft 0 CAP MVERTSTROKE
		include : HBarTop (eleft - O) RIGHTSB CAP
		include : HBar (eleft - O) (RIGHTSB - MVERTSTROKE / 4) (CAP * 0.54)
		include : HBarBottom (eleft - O) RIGHTSB 0
	alias 'cyrAE' 0x4D4 'AE'
	
	define [OEShape top] : glyph-construction
		local eleft MIDDLE
		
		# O half
		
		include : dispiro
			widths.lhs MVERTSTROKE 0
			straight.left.start eleft top [heading LEFTWARD]
			archv
			flat  SB (top - SMOOTHA)
			curl  SB SMOOTHB
			arcvh
			straight.right.end eleft 0 [heading RIGHTWARD]
		
		# E half
		include : VBarLeft eleft 0 top MVERTSTROKE
		include : HBarTop (eleft - O) RIGHTSB top
		include : HBar (eleft - O) (RIGHTSB - MVERTSTROKE / 4) (top * 0.54)
		include : HBarBottom (eleft - O) RIGHTSB 0
	
	create-glyph 'OE' : glyph-construction
		set-width WIDTH
		assign-unicode 0x152
		include capitalMarks
		include : OEShape CAP
	create-glyph 'smcpOE' : glyph-construction
		assign-unicode 0x276
		include eMarks
		include : OEShape XH
	
	define aeepart : glyph-construction
		local eLeft : MIDDLE - MVERTSTROKE / 2 * HVCONTRAST + 0.1
		local eMiddle : [mix eLeft (RIGHTSB - OX * 2) 0.5] - MVERTSTROKE * TANSLANT
		local barbottom (XH * EBARPOS - HALFSTROKE)
		
		local sma : SMALLSMOOTHA * 0.6
		local smb : SMALLSMOOTHB * 0.6
		include : dispiro
			widths.lhs MVERTSTROKE
			flat (RIGHTSB - OX * 2) barbottom [heading UPWARD]
			curl (RIGHTSB - OX * 2) (XH - smb)
			arcvh
			g4   eMiddle XO
			archv
			flat eLeft (XH - sma)
			curl eLeft smb
			hookend O true MVERTSTROKE
			g4 (RIGHTSB - OX * 2) (AHOOK + MVERTSTROKE * TANSLANT) [heading UPWARD]
		include : HBarBottom (eLeft + MVERTSTROKE / 2) (RIGHTSB - OX * 2 - MVERTSTROKE / 2) barbottom MVERTSTROKE
	
	define aeapart : glyph-construction
		local bartop (XH * BARPOS * 1.02 + HALFSTROKE)
		local abarRight : MIDDLE + MVERTSTROKE / 2 * HVCONTRAST - 0.1
		local lowmiddle : mix (SB + OX * 2) abarRight 0.5
		local barsmooth : mix SB abarRight 0.7
		
		local sma : SMALLSMOOTHA * 0.6
		local smb : SMALLSMOOTHB * 0.6
	
		include : dispiro
			widths.rhs MVERTSTROKE
			g4 (SB + OX) (XH - AHOOK - MVERTSTROKE * TANSLANT) [heading UPWARD]
			hookstart XO true MVERTSTROKE
			flat abarRight (XH - smb)
			curl abarRight smb
			arcvh
			g4 lowmiddle O [heading LEFTWARD]
			archv
			g4 (SB + OX * 2) [mix 0 bartop 0.45]
			arcvh
			flat barsmooth bartop
			curl (abarRight - 1) bartop [heading RIGHTWARD]
	
	define oeopart : glyph-construction
		local abarRight : MIDDLE + MVERTSTROKE / 2 * HVCONTRAST
		include : OShape XH 0 (SB + OX) (abarRight + OX) MVERTSTROKE (SMALLSMOOTHA * 0.6) (SMALLSMOOTHB * 0.6)
	
	create-glyph 'ae' : glyph-construction
		set-width WIDTH
		assign-unicode 0xE6
		include eMarks
		include aeepart
		include aeapart
	alias 'cyrae' 0x4D5 'ae'
	turned nothing 0x1D02 'ae' MIDDLE (XH / 2)
	
	
	create-glyph 'oe' : glyph-construction
		set-width WIDTH
		assign-unicode 0x153
		include eMarks
		include aeepart
		include oeopart
	turned nothing 0x1D14 'oe' MIDDLE (XH / 2)
	
	create-glyph 'db' : glyph-construction
		set-width WIDTH
		assign-unicode 0x238
		include bMarks
		include oeopart
		include : FlipAround MIDDLE (XH / 2)
		include oeopart
		include : VBar MIDDLE (XH / 2) CAP MVERTSTROKE
		if SLAB : begin
			include : LeftwardTopSerif (MIDDLE - MVERTSTROKE / 2 * HVCONTRAST) CAP SIDEJUT
	
	create-glyph 'qp' : glyph-construction
		set-width WIDTH
		assign-unicode 0x239
		include pMarks
		include oeopart
		include : FlipAround MIDDLE (XH / 2)
		include oeopart
		include : VBar MIDDLE DESCENDER (XH / 2) MVERTSTROKE
		if SLAB : begin
			include : CenterBottomSerif MIDDLE DESCENDER JUT
	
	create-glyph 'OU' : glyph-construction
		assign-unicode 0x222
		include capitalMarks
		include : OShape (CAP * BARPOS + HALFSTROKE) 0 SB RIGHTSB nothing SMOOTHA SMOOTHB
		include : dispiro
			widths.lhs
			flat SB CAP [heading DOWNWARD]
			curl SB (CAP * BARPOS - O - HALFSTROKE + SMOOTHB)
			arcvh
			g4 (MIDDLE + CORRECTION_OMIDS) (CAP * BARPOS - O - HALFSTROKE + (STROKE * (1 - CTHIN))) [widths.lhs (STROKE * CTHIN)]
			archv
			flat RIGHTSB (CAP * BARPOS - O - HALFSTROKE + SMOOTHA) [widths.lhs]
			curl RIGHTSB CAP [heading UPWARD]
	
	create-glyph 'ou' : glyph-construction
		assign-unicode 0x223
		include bMarks
		local bar : linreg 18 (XH * 0.9) 108 XH STROKE
		include : OShape bar 0 SB RIGHTSB
		include : dispiro
			widths.lhs
			flat SB CAP [heading DOWNWARD]
			curl SB (bar - O - STROKE + SMOOTHB)
			arcvh
			g4 (MIDDLE + CORRECTION_OMIDS) (bar - O - STROKE + (STROKE * (1 - CTHIN))) [widths.lhs (STROKE * CTHIN)]
			archv
			flat RIGHTSB (bar - O - STROKE + SMOOTHA) [widths.lhs]
			curl RIGHTSB CAP [heading UPWARD]
	
	create-glyph 'IJ' : glyph-construction
		assign-unicode 0x132
		include capitalMarks
		include : create-glyph : glyph-construction
			include glyphs.'I.straight'
			apply-transform : Translate (SB - MIDDLE + HALFSTROKE * HVCONTRAST) 0
		include : create-glyph : glyph-construction
			include glyphs.'J.shorthook'
			apply-transform : Translate JBALANCE2 0
	
	create-glyph 'ij' : glyph-construction
		assign-unicode 0x133
		include ifMarks
		include : create-glyph : glyph-construction
			include glyphs.'dotlessi.straight' AS_BASE
			include glyphs.dotAbove
			apply-transform : Translate (SB * 1.5 - MIDDLE + HALFSTROKE * HVCONTRAST) 0
		include : create-glyph : glyph-construction
			include glyphs.'dotlessj.straight' AS_BASE
			include glyphs.dotAbove
			apply-transform : Translate (RIGHTSB - SB * 0.5 - MIDDLE - JBALANCE - HALFSTROKE * HVCONTRAST) 0

symbol-block 'Ezh'
	define [EzhShape top bot pleft hookless] : glyph-construction
		local cor : 1.2 * HVCONTRAST
		local yMidBar : mix bot top 0.6
		local ezhLeft : mix SB RIGHTSB [fallback pleft 0.2]
		local ezhRight : mix SB RIGHTSB 0.925
		include : HBarTop SB ezhRight top
		
		start-from ezhLeft yMidBar
		line-to (ezhLeft + STROKE * cor) (yMidBar - HALFSTROKE)
		line-to (ezhLeft + STROKE * cor) yMidBar
		line-to ezhRight (top - STROKE)
		line-to (ezhRight - STROKE * cor) (top - STROKE)
		reverse-last
		
		include : dispiro
			widths.rhs
			flat ezhLeft yMidBar [heading RIGHTWARD]
			curl (MIDDLE - CORRECTION_OMIDS) yMidBar
			archv
			g4   RIGHTSB [mix yMidBar bot : hookless || (SMALLSMOOTHB / (SMALLSMOOTHA + SMALLSMOOTHB))] [if [not hookless] important [heading DOWNWARD]]
			if hookless {} : list
				hookend bot
				g4 SB (bot + HOOK * (top - bot) / [fallback para.cap0 CAP])
		
		if SLAB : begin
			include : DownwardLeftSerif SB top VJUT
	
	define [RevEzhShape top bot pleft hookless] : glyph-construction
		local cor : 1.2 * HVCONTRAST
		local yMidBar : mix bot top 0.6
		local ezhRight : mix RIGHTSB SB [fallback pleft 0.2]
		local ezhLeft : mix RIGHTSB SB 0.925
		include : HBarTop ezhLeft RIGHTSB top
		
		start-from ezhRight yMidBar
		line-to (ezhRight - STROKE * cor) (yMidBar - HALFSTROKE)
		line-to (ezhRight - STROKE * cor) yMidBar
		line-to ezhLeft (top - STROKE)
		line-to (ezhLeft + STROKE * cor) (top - STROKE)
		
		include : dispiro
			widths.lhs
			flat ezhRight yMidBar [heading LEFTWARD]
			curl (MIDDLE + CORRECTION_OMIDS) yMidBar
			archv
			g4   SB [mix yMidBar bot (SMALLSMOOTHA / (SMALLSMOOTHA + SMALLSMOOTHB))] [if [not hookless] important [heading DOWNWARD]]
			if hookless {} : list
				hookend bot
				g4 RIGHTSB (bot + HOOK * ((top - bot) / CAP))
		if SLAB : begin
			include : DownwardRightSerif RIGHTSB top VJUT
	
	create-glyph 'Ezh' : glyph-construction
		set-width WIDTH
		assign-unicode 0x1B7
		include capitalMarks
		include : EzhShape CAP 0
	alias 'cyrEzh' 0x4E0 'Ezh'
	
	create-glyph 'ezh' : glyph-construction
		set-width WIDTH
		assign-unicode 0x292
		include pMarks
		include : EzhShape XH DESCENDER
	alias 'cyrezh' 0x4E1 'ezh'
	
	create-glyph 'revEzh' : glyph-construction
		assign-unicode 0x1B8
		include capitalMarks
		include : RevEzhShape CAP 0
	
	create-glyph 'revezh' : glyph-construction
		assign-unicode 0x1B9
		include pMarks
		include : RevEzhShape XH DESCENDER
	
	create-glyph 'lyogh' : glyph-construction
		assign-unicode 0x26E
		include ifMarks
		include : EzhShape XH DESCENDER 0.4
		include : VBarLeft SB (XH * 0.1) CAP
	
	create-glyph 'ezhtail' : glyph-construction
		assign-unicode 0x1BA
		local b : mix DESCENDER XH 0.25
		include : EzhShape XH b nothing 0.5
		local y : mix [mix b XH 0.6] b 0.5
		include : dispiro
			widths.rhs
			g4.down.start RIGHTSB y [heading DOWNWARD]
			arcvh
			flat [mix SB RIGHTSB 0.45] b
			curl [mix SB RIGHTSB 0.4] b
			archv
			g4 (SB + STROKE * HVCONTRAST) [mix (DESCENDER + STROKE) b 0.5]
			arcvh
			flat [mix SB RIGHTSB 0.4] (DESCENDER + STROKE)
			curl RIGHTSB (DESCENDER + STROKE)
	
	create-glyph 'ezhcurlytail' : glyph-construction
		assign-unicode 0x293
		include eMarks
		local p (SMALLSMOOTHB / (SMALLSMOOTHA + SMALLSMOOTHB))
		include : EzhShape XH DESCENDER nothing p
		local fine : adviceBlackness 3
		local rinner : (XH * 0.4 - fine * 1.5) / 2
		local m1 : RIGHTSB - STROKE * HVCONTRAST
		local x2 : RIGHTSB - HALFSTROKE
		local y2 DESCENDER
		include : dispiro
			widths.lhs
			straight.down.start m1 [mix [mix DESCENDER XH 0.6] DESCENDER p] [heading DOWNWARD]
			CurlyTail fine rinner m1 DESCENDER SB x2 y2

symbol-block 'Glottal stop'
	create-glyph 'glottalstop' : glyph-construction
		assign-unicode 0x294
		include bMarks
		include : dispiro
			widths.rhs
			g4 SB (CAP - HOOK)
			hookstart CAPO
			g4 RIGHTSB (CAP - [adviceGlottalStopSmooth CAP 1])
			alsothru 0.5 0.5 important
			flat (MIDDLE + HALFSTROKE * HVCONTRAST) (XH * 0.3)
			curl (MIDDLE + HALFSTROKE * HVCONTRAST) 0 [heading DOWNWARD]
		if SLAB : begin
			include : CenterBottomSerif MIDDLE 0 JUT
	
	create-glyph 'revglottalstop' : glyph-construction
		assign-unicode 0x295
		include bMarks
		include : dispiro
			widths.lhs
			g4 RIGHTSB (CAP - HOOK)
			hookstart CAPO
			g4 SB (CAP - [adviceGlottalStopSmooth CAP (-1)])
			alsothru 0.5 0.5 important
			flat (MIDDLE - HALFSTROKE * HVCONTRAST) (XH * 0.3)
			curl (MIDDLE - HALFSTROKE * HVCONTRAST) 0 [heading DOWNWARD]
		if SLAB : begin
			include : CenterBottomSerif MIDDLE 0 JUT

	create-glyph 'smallglottalstop' : glyph-construction
		assign-unicode 0x242
		include bMarks
		include : dispiro
			widths.rhs
			g4 SB (XH - HOOK)
			hookstart XO
			g4 RIGHTSB (XH - [adviceGlottalStopSmooth XH 1])
			alsothru 0.5 0.5 important
			flat (MIDDLE + HALFSTROKE * HVCONTRAST) (XH * 0.15)
			curl (MIDDLE + HALFSTROKE * HVCONTRAST) 0 [heading DOWNWARD]
		if SLAB : begin
			include : CenterBottomSerif MIDDLE 0 JUT
	
	create-glyph 'invglottalstopbar' : glyph-construction
		assign-unicode 0x1BE
		include eMarks
		local smooth : adviceGlottalStopSmooth XH (-1 - TANSLANT * (15 - (WIDTH / 500) * 12) * [clamp 0 1 : linreg 126 1 135 0.5 STROKE])
		include : dispiro
			widths.lhs
			g4 RIGHTSB (XH - HOOK)
			hookstart XO
			g4.down.mid SB (XH - smooth)
			alsothru 0.5 0.5 important
			flat (MIDDLE - HALFSTROKE * HVCONTRAST) (XH * 0.15)
			curl (MIDDLE - HALFSTROKE * HVCONTRAST) 0 [heading DOWNWARD]
		include : HOverlayBar (MIDDLE - LONGJUT * 0.6) (MIDDLE + LONGJUT * 0.6) (XH * 0.25)
		include : FlipAround MIDDLE (XH / 2)
	
	create-glyph 'fineglottalstop' : glyph-construction
		include bMarks
		local fine : markHalfStroke * 2
		include : dispiro
			widths.rhs fine
			g4 SB (CAP - HOOK)
			hookstart CAPO
			g4 RIGHTSB (CAP - [adviceGlottalStopSmooth CAP 1])
			alsothru 0.5 0.5 important
			flat (MIDDLE + fine / 2 * HVCONTRAST) (XH * 0.3)
			curl (MIDDLE + fine / 2 * HVCONTRAST) 0 [heading DOWNWARD]
		if SLAB : begin
			include : CenterBottomSerif MIDDLE 0 JUT fine
	
	create-glyph 'finerevglottalstop' : glyph-construction
		include bMarks
		local fine : markHalfStroke * 2
		include : dispiro
			widths.lhs fine
			g4 RIGHTSB (CAP - HOOK)
			hookstart CAPO
			g4 SB (CAP - [adviceGlottalStopSmooth CAP (-1)])
			alsothru 0.5 0.5 important
			flat (MIDDLE - fine / 2 * HVCONTRAST) (XH * 0.3)
			curl (MIDDLE - fine / 2 * HVCONTRAST) 0 [heading DOWNWARD]
		if SLAB : begin
			include : CenterBottomSerif MIDDLE 0 JUT fine
	alias 'capglottalstop' 0x241 'glottalstop'
	turned 'invglottalstop' 0x296 'revglottalstop' MIDDLE (CAP / 2)

	create-glyph 'glottalstopbar' : glyph-construction
		assign-unicode 0x2A1
		include glyphs.glottalstop AS_BASE
		include : HOverlayBar (MIDDLE - LONGJUT * 0.6) (MIDDLE + LONGJUT * 0.6) (CAP * 0.25)
	
	create-glyph 'revglottalstopbar' : glyph-construction
		assign-unicode 0x2A2
		include glyphs.revglottalstop AS_BASE
		include : HOverlayBar (MIDDLE - LONGJUT * 0.6) (MIDDLE + LONGJUT * 0.6) (CAP * 0.25)

symbol-block 'eth' : create-glyph 'eth' : glyph-construction
	set-width WIDTH
	assign-unicode 0xF0
	include bMarks
	
	include : OShape (CAP * 0.6) 0 SB RIGHTSB
	local ymiddlea : (CAP * 0.6 + SMALLSMOOTHA - SMALLSMOOTHB) / 2
	include : dispiro
		widths.lhs
		g4.up.start (RIGHTSB - OX) ymiddlea
		quadcontrols 0 0.8
		g4 (SB + STROKE * 1.1) CAP
	
	include : dispiro
		widths.center OVERLAYSTROKE
		flat [mix SB RIGHTSB 0.1] [mix XH CAP (-0.1)]
		curl [mix SB RIGHTSB 0.95] [mix XH CAP 0.3]

symbol-block 'Thorn'
	create-glyph 'Thorn' : glyph-construction
		set-width WIDTH
		assign-unicode 0xDE
		include capitalMarks
		
		local bowlTop (CAP * 0.81)
		local bowlBottom (CAP * 0.19)
		
		local turn : mix bowlTop bowlBottom (SMOOTHB / (SMOOTHA + SMOOTHB))
		local turnRadius : (bowlTop - bowlBottom) / 2
		include : dispiro
			widths.rhs
			flat (SB * 1.25 + 1) bowlTop [heading RIGHTWARD]
			curl (RIGHTSB - turnRadius - CORRECTION_OMIDS) bowlTop
			archv
			g4   (RIGHTSB - O) turn
			arcvh
			flat (RIGHTSB - turnRadius + CORRECTION_OMIDS) bowlBottom
			curl (SB * 1.25 + 1) bowlBottom [heading LEFTWARD]
		
		include : VBarLeft (SB * 1.25) 0 CAP
		if SLAB : begin
			include : CenterBottomSerif (SB * 1.25 + HALFSTROKE * HVCONTRAST) 0 JUT
			include : CenterTopSerif (SB * 1.25 + HALFSTROKE * HVCONTRAST) CAP JUT
	
	create-glyph 'thorn' : glyph-construction
		assign-unicode 0xFE
		
		include : create-glyph : glyph-construction
			include glyphs.b
			eject-contour 'serifLB'
		include : create-glyph : glyph-construction
			include glyphs.p
			eject-contour 'bowl'
			eject-contour 'serifLT'
		include ifMarks

symbol-block 'Hwair'
	create-glyph 'Hwair' : glyph-construction
		assign-unicode 0x1F6
		include : VBarLeft (SB + O) 0 CAP MVERTSTROKE
		include : HBar SB MIDDLE (CAP / 2)
		include : dispiro
			widths.lhs MVERTSTROKE
			flat (MIDDLE - MVERTSTROKE * 0.5 * HVCONTRAST) CAP [heading DOWNWARD]
			curl (MIDDLE - MVERTSTROKE * 0.5 * HVCONTRAST) (SMALLSMOOTHB * 0.6)
			arcvh
			g4 [mix (MIDDLE - MVERTSTROKE * 0.5 * HVCONTRAST) (RIGHTSB - O) 0.5] O [heading RIGHTWARD]
			archv
			flat (RIGHTSB - O) (SMALLSMOOTHA * 0.6)
			curl (RIGHTSB - O) XH [heading UPWARD]
		if SLAB : begin
			local jut : JUT * 0.75
			include : CenterTopSerif (SB + O + MVERTSTROKE / 2 * HVCONTRAST) CAP jut
			include : CenterBottomSerif (SB + O + MVERTSTROKE / 2 * HVCONTRAST) 0 jut
			include : CenterTopSerif MIDDLE CAP jut
	
	create-glyph 'hwair' : glyph-construction
		assign-unicode 0x195
		include : VBarLeft (SB + O) 0 CAP MVERTSTROKE
		include : dispiro
			nShoulderKnots (SB + O + MVERTSTROKE * HVCONTRAST) (MIDDLE + MVERTSTROKE * 0.5 * HVCONTRAST) (MVERTSTROKE * 0.4) nothing (XH * 0.51) (SMALLSMOOTHA * 0.6) (SMALLSMOOTHB * 0.6) MVERTSTROKE
			flat (MIDDLE + MVERTSTROKE * 0.5 * HVCONTRAST) (XH * 0.5) [heading DOWNWARD]
			curl (MIDDLE + MVERTSTROKE * 0.5 * HVCONTRAST) (SMALLSMOOTHB * 0.6)
			arcvh
			g4 [mix (MIDDLE - MVERTSTROKE * 0.5 * HVCONTRAST) (RIGHTSB - O) 0.5] O [widths.heading MVERTSTROKE 0 RIGHTWARD]
			archv
			flat (RIGHTSB - O) (SMALLSMOOTHA * 0.6)
			curl (RIGHTSB - O) XH [heading UPWARD]
		if SLAB : begin
			local jut : JUT * 0.75
			include : LeftwardTopSerif (SB + O) CAP (jut - MVERTSTROKE / 2 * HVCONTRAST)
			if (!para.isItalic) : begin
				include : CenterBottomSerif (SB + O + MVERTSTROKE / 2 * HVCONTRAST) 0 jut
				tag-contour 'serifLB'

symbol-block 'Gha'
	create-glyph 'Gha' : glyph-construction
		assign-unicode 0x1A2
		include capitalMarks
		local abarRight : MIDDLE + MVERTSTROKE / 2 * HVCONTRAST
		include : OShape CAP 0 (SB + O) abarRight MVERTSTROKE (SMALLSMOOTHA * 0.6) (SMALLSMOOTHB * 0.6)
		include : VBarRight (RIGHTSB - O * 2) DESCENDER CAP MVERTSTROKE
		include : dispiro
			widths.lhs MVERTSTROKE
			flat MIDDLE (CAP - SMALLSMOOTHB * 0.6) [heading RIGHTWARD]
			curl (MIDDLE + 1) (CAP - SMALLSMOOTHB * 0.6) [heading RIGHTWARD]
			alsothru 0.5 0.15
			g4   (RIGHTSB - O * 2 - MVERTSTROKE * HVCONTRAST) CAP [widths 0 MVERTSTROKE]
	
	
	create-glyph 'gha' : glyph-construction
		assign-unicode 0x1A3
		include pMarks
		local abarRight : MIDDLE + MVERTSTROKE / 2 * HVCONTRAST
		include : OShape XH 0 (SB + O) abarRight MVERTSTROKE (SMALLSMOOTHA * 0.6) (SMALLSMOOTHB * 0.6)
		include : VBarRight (RIGHTSB - O * 2) DESCENDER XH MVERTSTROKE
		include : dispiro
			widths.lhs MVERTSTROKE
			flat MIDDLE (XH - SMALLSMOOTHB * 0.6) [heading RIGHTWARD]
			curl (MIDDLE + 1) (XH - SMALLSMOOTHB * 0.6) [heading RIGHTWARD]
			alsothru 0.5 0.15
			g4   (RIGHTSB - O * 2 - MVERTSTROKE * HVCONTRAST) XH [widths 0 MVERTSTROKE]

symbol-block 'Wynn'
	define [WynnShape bot top] : glyph-construction
		include : VBarLeft SB bot top
		include : dispiro
			widths.rhs (STROKE * CTHIN)
			g4 (SB + HVCONTRAST * STROKE * (1 - CTHIN)) [mix bot top 0.8]
			alsothru 0.55 0.85
			g4 MIDDLE (top - O) [widths.heading 0 STROKE RIGHTWARD]
			archv
			g4 (RIGHTSB - O) [mix bot top 0.7]
			alsothru 0.25 0.45
			g4 (SB + STROKE * HVCONTRAST) [mix bot top 0.1]
	
	create-glyph 'Wynn' : glyph-construction
		assign-unicode 0x1F7
		include capitalMarks
		include : WynnShape 0 CAP
	
	create-glyph 'wynn' : glyph-construction
		assign-unicode 0x1BF
		include pMarks
		include : WynnShape DESCENDER XH

symbol-block 'Yogh'
	define [YoghShape top bot] : glyph-construction
		include : dispiro
			widths.rhs
			g4 SB (top - HOOK)
			hookstart (top - O)
			g4 RIGHTSB (top - SMOOTHB)
			alsothruthem {{0.5 0.75}}
			g4 ([mix SB RIGHTSB 0.1] + STROKE * 0.25) ([mix bot top 0.425] - HALFSTROKE)
		include : dispiro
			widths.rhs
			g4 [mix SB RIGHTSB 0.75] ([mix bot (top - SMOOTHB) 0.758] + HALFSTROKE)
			g4 RIGHTSB ([mix bot (top - SMOOTHB) 0.475])
			alsothruthem {{0.25 0.6} {0.5 0.81}}
			g4 SB (bot + O)
	
	create-glyph 'Yogh' : glyph-construction
		assign-unicode 0x21C
		include capitalMarks
		include : YoghShape CAP 0
	
	create-glyph 'yogh' : glyph-construction
		assign-unicode 0x21D
		include pMarks
		include : YoghShape XH DESCENDER

symbol-block 'RamsHorn'
	define [RamsHornShape bottom top] : glyph-construction
		local hf : [adviceBlackness 4] / 2
		local d : hf * 2 + WIDTH * 0.05
	
		include : dispiro
			widths.center
			g4 (SB + HALFSTROKE * HVCONTRAST + O) top [heading DOWNWARD]
			quadcontrols 1 0.7 16
			g4 (MIDDLE + d - hf) (bottom + d * (1 - TANSLANT * 0.5)) [widths hf hf]
			arcvh
			g4 (MIDDLE + CORRECTION_OMIDX * hf * 2) (bottom + hf)
			archv
			g4 (MIDDLE - d + hf) (bottom + d * (1 + TANSLANT * 0.5))
			quadcontrols 0 0.3 16
			g4 (RIGHTSB - HALFSTROKE * HVCONTRAST - O) top [widths.heading HALFSTROKE HALFSTROKE UPWARD]
	
	create-glyph 'latingamma' : glyph-construction
		assign-unicode 0x263
		include pMarks
		include : RamsHornShape DESCENDER XH
	
	create-glyph 'latinGamma' : glyph-construction
		assign-unicode 0x194
		include ifMarks
		include : RamsHornShape DESCENDER CAP
	
	create-glyph 'ramshorn' : glyph-construction
		assign-unicode 0x264
		include eMarks
		include : RamsHornShape 0 XH

symbol-block 'Rhotic Tail and other phonetic letters'
	define [ErTail left w dohook] : glyph-construction
		local right WIDTH
		local mid : mix left right 0.5
		local rise : (WIDTH - left) * 0.3
		local sw : fallback w (markFine * 2)
		include : dispiro
			widths.center sw
			g2 (left) (XH * 0.5)
			g2 (mid - sw * HVCONTRAST) (XH * 0.5 + rise) [widths 0 sw]
		include : dispiro
			widths.rhs sw
			flat mid (XH * 0.5 + rise) [heading DOWNWARD]
			curl mid (XH * 0.5 + [if dohook 0 (rise - 1)]) [heading DOWNWARD]
			if dohook {[hookend (XH * 0.5 - rise)]} {[arcvh]}
			g4 (right - [if dohook (markFine * 2) 0]) (XH * 0.5 - [if dohook (rise * 0.5) rise]) [if dohook nothing [heading RIGHTWARD]]
			#if dohook {[hookend (XH * 0.5 - rise)] [g4 right (XH * 0.5)]} {}
	
	create-glyph 'rhotichook' : glyph-construction
		assign-unicode 0x2DE
		include : ErTail (-STROKE * HVCONTRAST) STROKE true
	
	if [not recursive] : let [thinfont : Widen {'schwa' 'revlatinepsilon'} 0.85 1] : begin
		create-glyph 'er' : glyph-construction
			assign-unicode 0x25A
			include eMarks
			include thinfont.schwa
			include : ErTail (WIDTH * 0.85 - SB - markFine * HVCONTRAST * 1.25)
		create-glyph 'revlatinepsiloner' : glyph-construction
			assign-unicode 0x25D
			include eMarks
			include thinfont.revlatinepsilon
			include : ErTail (WIDTH * 0.85 - SB - markFine * HVCONTRAST * 1.25)
	
	create-glyph 'bidentalpercussive' : glyph-construction
		assign-unicode 0x2AD
		local g : create-glyph : glyph-construction
			include : HBarTop SB RIGHTSB (CAP * 0.4)
			include : VBarLeft SB (CAP * 0.1) (CAP * 0.4)
			include : VBarRight RIGHTSB (CAP * 0.1) (CAP * 0.4)
		include g
		apply-transform : Upright
		apply-transform : Translate 0 (CAP / 2)
		apply-transform : Italify
		include g