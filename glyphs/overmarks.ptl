
### Combining marks

local markExtend (ACCENTX * 0.5)
local markStress : 0.5 * [adviceBlackness 3.2]
local markFine : markStress * 0.8
local markHalfStroke : mix markFine markStress 0.5
local markMiddle (-MIDDLE)
local markDotsRadius : DOTRADIUS * markStress / HALFSTROKE

local aboveMarkTop (XH + ACCENT * 1.38)
local aboveMarkBot (XH + ACCENT * 0.35)

local belowMarkBot (0 - ACCENT * 1.38)
local belowMarkTop (0 - ACCENT * 0.35)

local commaOvershoot : O * [linreg 16 1 90 (-1) (markHalfStroke * 2)]

### Above marks
symbol-block 'Above marks'
	create-glyph 'dotAbove' : glyph-construction
		set-width 0
		assign-unicode 0x307
		this.shortName = 'dot'
		set-anchor 'above' MARK markMiddle XH markMiddle aboveMarkTop
		include : Ring (XH + ACCENT + DOTRADIUS) (XH + ACCENT - DOTRADIUS) (markMiddle - DOTRADIUS) (markMiddle + DOTRADIUS) true
	
	create-glyph 'dieresisAbove' : glyph-construction
		set-width 0
		assign-unicode 0x308
		this.shortName = 'dieresis'
		set-anchor 'above' MARK markMiddle XH markMiddle aboveMarkTop
		include : Ring (XH + ACCENT + markDotsRadius) (XH + ACCENT - markDotsRadius) (markMiddle - markDotsRadius - markExtend) (markMiddle + markDotsRadius - markExtend) true
		include : Ring (XH + ACCENT + markDotsRadius) (XH + ACCENT - markDotsRadius) (markMiddle - markDotsRadius + markExtend) (markMiddle + markDotsRadius + markExtend) true
	
	create-glyph 'ringAbove' : glyph-construction
		set-width 0
		assign-unicode 0x30A
		this.shortName = 'ring'
		set-anchor 'above' MARK markMiddle XH markMiddle aboveMarkTop
		
		local radiusIn  : ACCENT * 0.24
		local radiusOut : radiusIn + markFine * [linreg 16 2 90 1.75 (markHalfStroke * 2)]
		include : Ring (XH + ACCENT + radiusOut) (XH + ACCENT - radiusOut) (markMiddle - radiusOut) (markMiddle + radiusOut) true
		include : Ring (XH + ACCENT + radiusIn) (XH + ACCENT - radiusIn) (markMiddle - radiusIn) (markMiddle + radiusIn) true
		reverse-last
		apply-transform : Upright
		apply-transform : Translate 0 (-markFine * 0.75)
		apply-transform : Italify
	
	create-glyph 'graveAbove' : glyph-construction
		set-width 0
		assign-unicode 0x300
		this.shortName = 'grave'
		set-anchor 'above' MARK markMiddle XH markMiddle aboveMarkTop
		
		include : dispiro
			flat (markMiddle + markStress) aboveMarkBot [widths markFine markFine]
			curl (markMiddle - markExtend) aboveMarkTop [widths markStress markStress]
	
	create-glyph 'acuteAbove' : glyph-construction
		set-width 0
		assign-unicode 0x301
		this.shortName = 'acute'
		set-anchor 'above' MARK markMiddle XH markMiddle aboveMarkTop
		
		include : dispiro
			flat (markMiddle - markStress) aboveMarkBot [widths markFine markFine]
			curl (markMiddle + markExtend) aboveMarkTop [widths markStress markStress]
	
	create-glyph 'circumflexAbove' : glyph-construction
		set-width 0
		assign-unicode 0x302
		this.shortName = 'circum'
		set-anchor 'above' MARK markMiddle XH markMiddle aboveMarkTop
		
		include : dispiro 
			flat (markMiddle - markExtend - markStress) (aboveMarkBot + markStress - markFine) [widths markStress markStress]
			curl markMiddle (aboveMarkTop + markFine * 0.7) [heading UPWARD]
		include : dispiro
			flat (markMiddle + markExtend + markStress) (aboveMarkBot + markStress - markFine) [widths markStress markStress]
			curl markMiddle (aboveMarkTop + markFine * 0.7) [heading UPWARD]
	
	create-glyph 'caronAbove' : glyph-construction
		set-width 0
		assign-unicode 0x30c
		this.shortName = 'caron'
		set-anchor 'above' MARK markMiddle XH markMiddle aboveMarkTop
		
		include : dispiro
			flat (markMiddle - markExtend - markStress) aboveMarkTop [widths markStress markStress]
			curl markMiddle (aboveMarkBot - markFine * 1.7 + markStress) [heading DOWNWARD]
		include : dispiro
			flat (markMiddle + markExtend + markStress) aboveMarkTop [widths markStress markStress]
			curl markMiddle (aboveMarkBot - markFine * 1.7 + markStress) [heading DOWNWARD]
	
	create-glyph 'tildeAbove' : glyph-construction
		set-width 0
		assign-unicode 0x303
		this.shortName = 'tilde'
		set-anchor 'above' MARK markMiddle XH markMiddle aboveMarkTop
		
		local leftEnd (markMiddle - markExtend * 1.5)
		local rightEnd (markMiddle + markExtend * 1.5)
		
		local ttop aboveMarkTop
		local tbot : aboveMarkBot + markFine / 2
		local top : ttop + (markFine * 2)
		local bot : tbot - (markFine * 2)
		local tildeWave : [linreg 16 2.6 90 1.36 (markHalfStroke * 2)] * (1 + [linreg 100 0 125 1 markExtend] * [clamp 0 1 : linreg 91 0 95 0.05 (markHalfStroke * 2)])
		local tildeWaveX [linreg 16 0.5 90 0.52 (markHalfStroke * 2)]
		local tildeWaveEnd : [smoothreg {16 60 90} {(-0.025) (-0.05) 0}] (markHalfStroke * 2)
		
		include : dispiro
			widths.center (markHalfStroke * 2 / CONTRAST)
			g2 leftEnd [mix tbot ttop tildeWaveEnd]
			bezcontrols.absolute 
				* [mix leftEnd rightEnd tildeWaveX]; * [mix bot top tildeWave]
				* [mix leftEnd rightEnd (1 - tildeWaveX)]; * [mix bot top (1 - tildeWave)]
				* 256; * important; * g2
			g2 rightEnd [mix tbot ttop (1 - tildeWaveEnd)]
			#end [function : set this.dense true]
		
		set this.dense true
	
	create-glyph 'macronAbove' : glyph-construction
		set-width 0
		assign-unicode 0x304
		this.shortName = 'macron'
		set-anchor 'above' MARK markMiddle XH markMiddle aboveMarkTop
		
		local leftEnd (markMiddle - markExtend * 1.5)
		local rightEnd (markMiddle + markExtend * 1.5)
		
		include : dispiro
			flat leftEnd (XH + ACCENT) [widths markHalfStroke markHalfStroke]
			curl rightEnd (XH + ACCENT)
	
	create-glyph 'overlineAbove' : glyph-construction
		set-width 0
		assign-unicode 0x305
		this.shortName = 'overline'
		set-anchor 'above' MARK markMiddle XH markMiddle aboveMarkTop
		
		include : dispiro
			flat (SB - WIDTH) (XH + ACCENT) [widths markHalfStroke markHalfStroke]
			curl (RIGHTSB - WIDTH) (XH + ACCENT)
	
	create-glyph 'dblOverlineAbove' : glyph-construction
		set-width 0
		assign-unicode 0x33F
		set-anchor 'above' MARK markMiddle XH markMiddle aboveMarkTop
		
		local boxsw : Math.min (markFine * 2) ((aboveMarkTop - aboveMarkBot) / 3)
		
		include : HBarBottom (SB - WIDTH) (RIGHTSB - WIDTH) aboveMarkBot boxsw
		include : HBarTop (SB - WIDTH) (RIGHTSB - WIDTH) aboveMarkTop boxsw
	
	create-glyph 'breveAbove' : glyph-construction
		set-width 0
		assign-unicode 0x306
		this.shortName = 'breve'
		set-anchor 'above' MARK markMiddle XH markMiddle aboveMarkTop
		
		local leftEnd (markMiddle - markExtend * 1.2)
		local rightEnd (markMiddle + markExtend * 1.2)
		
		include : dispiro
			g4.down.start leftEnd aboveMarkTop [widths.heading markHalfStroke markHalfStroke DOWNWARD]
			arcvh
			g4.right.mid markMiddle (aboveMarkBot + markHalfStroke) [heading RIGHTWARD]
			archv
			g4.up.end rightEnd aboveMarkTop [heading UPWARD]
	
	create-glyph 'archAbove' : glyph-construction
		set-width 0
		assign-unicode 0x311
		this.shortName = 'arch'
		set-anchor 'above' MARK markMiddle XH markMiddle aboveMarkTop
		
		local leftEnd (markMiddle - markExtend * 1.2)
		local rightEnd (markMiddle + markExtend * 1.2)
		
		include : dispiro
			g4.up.start leftEnd aboveMarkBot [widths.heading markHalfStroke markHalfStroke UPWARD]
			arcvh
			g4.right.mid markMiddle (aboveMarkTop - markHalfStroke) [heading RIGHTWARD]
			archv
			g4.down.end rightEnd aboveMarkBot [heading DOWNWARD]
	
	create-glyph 'hookAbove' : glyph-construction
		set-width 0
		assign-unicode 0x309
		this.shortName = 'hook'
		set-anchor 'above' MARK markMiddle XH markMiddle aboveMarkTop
		
		local fine : Math.min markFine ((aboveMarkTop - aboveMarkBot) * 0.2)
		
		local hookBot (aboveMarkBot - fine / 2)
		local hookTop ([mix aboveMarkBot aboveMarkTop 0.9] + fine / 2)
		include : dispiro
			widths.lhs (fine * 2)
			flat (markMiddle - fine * HVCONTRAST) hookBot [heading RIGHTWARD]
			curl (markMiddle + fine * 0.4) hookBot [heading RIGHTWARD]
			archv
			g4.up.mid (markMiddle + markExtend - O) [mix hookBot hookTop (SMOOTHB / (SMOOTHB + SMOOTHA))] [heading UPWARD]
			arcvh
			flat (markMiddle + fine * 0.4) hookTop [heading LEFTWARD]
			curl (markMiddle - markExtend + fine) hookTop [heading LEFTWARD]
	
	create-glyph 'doubleGraveAbove' : glyph-construction
		set-width 0
		assign-unicode 0x30F
		this.shortName = 'dblgrave'
		set-anchor 'above' MARK markMiddle XH markMiddle aboveMarkTop
		
		local m1 : markMiddle - markExtend * 0.85
		local m2 : markMiddle + markExtend * 0.85
		include : dispiro
			flat (m1 + markStress) aboveMarkBot [widths markFine markFine]
			curl (m1 - markExtend) aboveMarkTop [widths markStress markStress]
		include : dispiro
			flat (m2 + markStress) aboveMarkBot [widths markFine markFine]
			curl (m2 - markExtend) aboveMarkTop [widths markStress markStress]
	
	create-glyph 'doubleAcuteAbove' : glyph-construction
		set-width 0
		assign-unicode 0x30B
		this.shortName = 'dblacute'
		set-anchor 'above' MARK markMiddle XH markMiddle aboveMarkTop
		
		local m1 : markMiddle - markExtend * 0.85
		local m2 : markMiddle + markExtend * 0.85
		include : dispiro
			flat (m1 - markStress) aboveMarkBot [widths markFine markFine]
			curl (m1 + markExtend) aboveMarkTop [widths markStress markStress]
		include : dispiro
			flat (m2 - markStress) aboveMarkBot [widths markFine markFine]
			curl (m2 + markExtend) aboveMarkTop [widths markStress markStress]
	
	create-glyph 'commaAbove' : glyph-construction
		set-width 0
		assign-unicode 0x313
		this.shortName = 'comma'
		set-anchor 'above' MARK markMiddle XH markMiddle aboveMarkTop
		
		local radius : adviceBlackness 6
		include : Ring (aboveMarkTop + commaOvershoot) (aboveMarkTop - radius * 2 + commaOvershoot) (markMiddle - radius) (markMiddle + radius)
		include : dispiro
			widths.rhs : Math.min radius (markFine * [linreg 16 2 90 1.75 (markHalfStroke * 2)])
			straight.down.start (markMiddle + radius) (aboveMarkTop - radius + commaOvershoot)
			quadcontrols 0 0.6
			g4   [mix markMiddle (markMiddle - radius) 0.2] (aboveMarkBot - radius)
		apply-transform : Upright
		apply-transform : Translate 0 (DOTRADIUS * 0.3)
		apply-transform : Italify
	
	create-glyph 'turnCommaAbove' : glyph-construction
		set-width 0
		assign-unicode 0x312
		this.shortName = 'turnComma'
		include glyphs.commaAbove
		include : FlipAround markMiddle ([mix aboveMarkTop (aboveMarkBot - [adviceBlackness 6]) 0.5] + (DOTRADIUS * 0.3))
		set-anchor 'above' MARK markMiddle XH markMiddle aboveMarkTop
	
	create-glyph 'revCommaAbove' : glyph-construction
		set-width 0
		assign-unicode 0x314
		this.shortName = 'revComma'
		set-anchor 'above' MARK markMiddle XH markMiddle aboveMarkTop
		
		local radius : adviceBlackness 6
		include : Ring (aboveMarkTop + commaOvershoot) (aboveMarkTop - radius * 2 + commaOvershoot) (markMiddle - radius) (markMiddle + radius)
		include : dispiro
			widths.lhs : Math.min radius (markFine * [linreg 16 2 90 1.75 (markHalfStroke * 2)])
			straight.down.start (markMiddle - radius) (aboveMarkTop - radius + commaOvershoot)
			quadcontrols 0 0.6
			g4   [mix markMiddle (markMiddle + radius) 0.2] (aboveMarkBot - radius)
		apply-transform : Upright
		apply-transform : Translate 0 (DOTRADIUS * 0.3)
		apply-transform : Italify
	
	create-glyph 'barAbove' : glyph-construction
		set-width 0
		assign-unicode 0x30D
		set-anchor 'above' MARK markMiddle XH markMiddle aboveMarkTop
		include : VBar markMiddle aboveMarkBot aboveMarkTop (markHalfStroke * 2)
	
	create-glyph 'dblBarAbove' : glyph-construction
		set-width 0
		assign-unicode 0x30E
		set-anchor 'above' MARK markMiddle XH markMiddle aboveMarkTop
		include : VBar (markMiddle - markExtend * 0.75) aboveMarkBot aboveMarkTop (markHalfStroke * 2)
		include : VBar (markMiddle + markExtend * 0.75) aboveMarkBot aboveMarkTop (markHalfStroke * 2)
	
	create-glyph 'bridgeAbove' : glyph-construction
		set-width 0
		assign-unicode 0x346
		set-anchor 'above' MARK markMiddle XH markMiddle aboveMarkTop
		
		include : VBar (markMiddle - markExtend) aboveMarkBot aboveMarkTop (markFine * 2)
		include : VBar (markMiddle + markExtend) aboveMarkBot aboveMarkTop (markFine * 2)
		include : HBarTop (markMiddle - markExtend) (markMiddle + markExtend) aboveMarkTop (markFine * 2)
	
	create-glyph 'yerikAbove' : glyph-construction
		set-width 0
		assign-unicode 0x33E
		set-anchor 'above' MARK markMiddle XH markMiddle aboveMarkTop
		include : dispiro
			widths.center (markFine * 2)
			g4 (markMiddle - markFine * 0.5) aboveMarkBot [heading UPWARD]
			alsothruthem {{0.9 0.35} {0.1 0.65}} [heading UPWARD]
			g4 (markMiddle + markFine * 0.5) aboveMarkTop  [heading UPWARD]
	
	create-glyph 'candrabinduAbove' : glyph-construction
		set-width 0
		assign-unicode 0x310
		local fine : adviceBlackness 6.5
		local yc : XH + ACCENT
		local radius : Math.max (markExtend - DOTRADIUS) (fine * 1.35)
		include : dispiro
			widths.rhs : Math.min (markHalfStroke * 2) (fine * 1.25)
			g4.down.start (markMiddle - radius) yc [heading DOWNWARD]
			arcvh
			g4.right.mid markMiddle (yc - radius)
			archv
			g4.up.end (markMiddle + radius) yc [heading UPWARD]
		include : DotAt markMiddle (XH + ACCENT) fine
		apply-transform : Upright
		apply-transform : Translate 0 (fine * 0.5)
		apply-transform : Italify
		set-anchor 'above' MARK markMiddle XH markMiddle aboveMarkTop
	
	
	create-glyph 'variaAbove' : glyph-construction
		assign-unicode 0x340
		set-width 0
		set-anchor 'above' MARK markMiddle XH markMiddle aboveMarkTop
		
		include : dispiro
			flat (markMiddle + markStress) (aboveMarkBot - markFine * 0.5) [widths markFine markFine]
			curl (markMiddle - markExtend * 0.5) (aboveMarkTop + markStress * 0.25) [widths markStress markStress]
	
	create-glyph 'oxiaAbove' : glyph-construction
		assign-unicode 0x341
		set-width 0
		set-anchor 'above' MARK markMiddle XH markMiddle aboveMarkTop
		
		include : dispiro
			flat (markMiddle - markStress) (aboveMarkBot - markFine * 0.5) [widths markFine markFine]
			curl (markMiddle + markExtend * 0.5) (aboveMarkTop + markStress * 0.25) [widths markStress markStress]
	
	create-glyph 'perispomeniAbove' : glyph-construction
		set-width 0
		assign-unicode 0x342
		this.shortName = 'perisp'
		include glyphs.archAbove
		set-anchor 'above' MARK markMiddle XH markMiddle aboveMarkTop
	
	alias 'koronisAbove' 0x343 'commaAbove'
	create-glyph 'crossAbove' : glyph-construction
		set-width 0
		assign-unicode 0x33D
		set-anchor 'above' MARK markMiddle XH markMiddle aboveMarkTop
		
		include : dispiro
			flat (markMiddle - markExtend) aboveMarkTop [widths markFine markFine]
			curl (markMiddle + markExtend) aboveMarkBot
		include : dispiro
			flat (markMiddle - markExtend) aboveMarkBot [widths markFine markFine]
			curl (markMiddle + markExtend) aboveMarkTop


### Below marks
symbol-block 'Below marks'
	create-glyph 'cedillaBelow' : glyph-construction
		set-width 0
		assign-unicode 0x327
		set-anchor 'below' MARK markMiddle 0 markMiddle belowMarkBot
		
		local fine : Math.min markHalfStroke ((belowMarkTop - belowMarkBot) * 0.25)
		
		local cedillaTop (belowMarkTop + fine * 0.85)
		local cedillaBot belowMarkBot
		include : dispiro
			flat (markMiddle - fine * HVCONTRAST) cedillaTop [widths.heading 0 (fine * 2) RIGHTWARD]
			curl markMiddle cedillaTop [heading RIGHTWARD]
			archv
			g4   (markMiddle + markExtend - O) [mix cedillaTop cedillaBot 0.5] [heading DOWNWARD]
			arcvh
			flat markMiddle cedillaBot [heading LEFTWARD]
			curl (markMiddle - markExtend) cedillaBot [heading LEFTWARD]
		include : VBar markMiddle 0 (cedillaTop - fine) (fine * 2)
	
	create-glyph 'ogonekBelow' : glyph-construction
		set-width 0
		assign-unicode 0x328
		
		local fine : markHalfStroke * [linreg 16 1 90 0.2 (markHalfStroke * 2)]
		local ogonekBot : [mix belowMarkTop belowMarkBot 0.75] + markStress * 2
		local ogonekLeft : markMiddle - markExtend * 0.5
		local ogonekRight : markMiddle + markExtend * 0.85
		set-anchor 'below' MARK markMiddle 0 markMiddle belowMarkBot
		
		include : dispiro
			g4.left.start markMiddle 0 [widths 0 (fine * 2)]
			#archv
			g4   ogonekLeft [mix 0 ogonekBot 0.5] [widths 0 ([mix fine markStress 0.75] * 2)]
			#arcvh
			flat          (markMiddle + markExtend * 0.25) ogonekBot [widths 0 (markStress * 2)]
			curl          ogonekRight ogonekBot [heading RIGHTWARD]
	
	create-glyph 'ogonekTR' : glyph-construction
		set-width 0
		include glyphs.ogonekBelow
		local ogonekBot : [mix belowMarkTop belowMarkBot 0.75] + markStress * 2
		local ogonekRight : markMiddle + markExtend * 0.85
		set-anchor 'trailing' MARK markMiddle 0 ogonekRight ogonekBot
	
	create-glyph 'iotaBelow' : glyph-construction
		set-width 0
		assign-unicode 0x345
		set this.shortName 'iota'
		set-anchor 'below' MARK markMiddle 0 markMiddle belowMarkBot
		
		include : VBar markMiddle belowMarkBot belowMarkTop (markHalfStroke * 2)
	
	create-glyph 'iotaLF' : glyph-construction
		set-width 0
		set-anchor 'lf' MARK markMiddle 0 markMiddle belowMarkBot
		include : VBar markMiddle belowMarkBot belowMarkTop (markHalfStroke * 2)
	
	create-glyph 'uptackBelow' : glyph-construction
		set-width 0
		assign-unicode 0x31D
		set-anchor 'below' MARK markMiddle 0 markMiddle belowMarkBot
		
		include : VBar markMiddle belowMarkBot belowMarkTop (markFine * 2)
		include : HBarBottom (markMiddle - markExtend) (markMiddle + markExtend) belowMarkBot (markFine * 2)
	
	create-glyph 'downtackBelow' : glyph-construction
		set-width 0
		assign-unicode 0x31E
		set-anchor 'below' MARK markMiddle 0 markMiddle belowMarkBot
		
		include : VBar markMiddle belowMarkBot belowMarkTop (markFine * 2)
		include : HBarTop (markMiddle - markExtend) (markMiddle + markExtend) belowMarkTop (markFine * 2)
	
	create-glyph 'lefttackBelow' : glyph-construction
		set-width 0
		assign-unicode 0x318
		set-anchor 'below' MARK markMiddle 0 markMiddle belowMarkBot
		
		include : VBar (markMiddle + markExtend) belowMarkBot belowMarkTop (markFine * 2)
		include : HBar (markMiddle - markExtend) (markMiddle + markExtend) [mix belowMarkTop belowMarkBot 0.5] (markFine * 2)
	
	create-glyph 'righttackBelow' : glyph-construction
		set-width 0
		assign-unicode 0x319
		set-anchor 'below' MARK markMiddle 0 markMiddle belowMarkBot
		
		include : VBar (markMiddle - markExtend) belowMarkBot belowMarkTop (markFine * 2)
		include : HBar (markMiddle - markExtend) (markMiddle + markExtend) [mix belowMarkTop belowMarkBot 0.5] (markFine * 2)
	
	create-glyph 'leftAngleBelow' : glyph-construction
		set-width 0
		assign-unicode 0x349
		set-anchor 'below' MARK markMiddle 0 markMiddle belowMarkBot
		
		include : VBar (markMiddle + markExtend) belowMarkBot belowMarkTop (markFine * 2)
		include : HBarTop (markMiddle - markExtend) (markMiddle + markExtend) belowMarkTop (markFine * 2)
	
	create-glyph 'plusBelow' : glyph-construction
		set-width 0
		assign-unicode 0x31F
		set-anchor 'below' MARK markMiddle 0 markMiddle belowMarkBot
		
		include : VBar markMiddle belowMarkBot belowMarkTop (markFine * 2)
		include : HBar (markMiddle - markExtend) (markMiddle + markExtend) [mix belowMarkTop belowMarkBot 0.5] (markFine * 2)
	
	create-glyph 'minusBelow' : glyph-construction
		set-width 0
		assign-unicode 0x320
		set-anchor 'below' MARK markMiddle 0 markMiddle belowMarkBot
		
		include : VBar (markMiddle - markExtend) belowMarkBot belowMarkTop (markFine * 2)
		include : VBar (markMiddle + markExtend) belowMarkBot belowMarkTop (markFine * 2)
		include : HBar (markMiddle - markExtend) (markMiddle + markExtend) [mix belowMarkTop belowMarkBot 0.5] (markFine * 2)
	
	create-glyph 'bridgeBelow' : glyph-construction
		set-width 0
		assign-unicode 0x32A
		set-anchor 'below' MARK markMiddle 0 markMiddle belowMarkBot
		
		include : VBar (markMiddle - markExtend) belowMarkBot belowMarkTop (markFine * 2)
		include : VBar (markMiddle + markExtend) belowMarkBot belowMarkTop (markFine * 2)
		include : HBarTop (markMiddle - markExtend) (markMiddle + markExtend) belowMarkTop (markFine * 2)
	
	create-glyph 'boxBelow' : glyph-construction
		set-width 0
		assign-unicode 0x33B
		set-anchor 'below' MARK markMiddle 0 markMiddle belowMarkBot
		
		local boxsw : Math.min (markFine * 2) ((belowMarkTop - belowMarkBot) / 3)
		
		include : VBar (markMiddle - markExtend) belowMarkBot belowMarkTop (markFine * 2)
		include : VBar (markMiddle + markExtend) belowMarkBot belowMarkTop (markFine * 2)
		include : HBarBottom (markMiddle - markExtend) (markMiddle + markExtend) belowMarkBot boxsw
		include : HBarTop (markMiddle - markExtend) (markMiddle + markExtend) belowMarkTop boxsw
	
	create-glyph 'equalBelow' : glyph-construction
		set-width 0
		assign-unicode 0x347
		set-anchor 'below' MARK markMiddle 0 markMiddle belowMarkBot
		
		local boxsw : Math.min (markFine * 2) ((belowMarkTop - belowMarkBot) / 3)
		
		include : HBarBottom (markMiddle - markExtend) (markMiddle + markExtend) belowMarkBot boxsw
		include : HBarTop (markMiddle - markExtend) (markMiddle + markExtend) belowMarkTop boxsw
	
	create-glyph 'dblBreveBelow' : glyph-construction
		set-width 0
		assign-unicode 0x32B
		set-anchor 'below' MARK markMiddle 0 markMiddle belowMarkBot
		
		local extend : (belowMarkTop - belowMarkBot - markHalfStroke) * 1.25
		
		include : dispiro
			g4.down.start (markMiddle - extend) belowMarkTop [widths.heading markHalfStroke markHalfStroke DOWNWARD]
			arcvh
			g4.right.mid  [mix markMiddle (markMiddle - extend) 0.5] (belowMarkBot + markHalfStroke) [heading RIGHTWARD]
			archv
			g4.up.end     markMiddle belowMarkTop [heading UPWARD]
		include : dispiro
			g4.down.start markMiddle belowMarkTop [widths.heading markHalfStroke markHalfStroke DOWNWARD]
			arcvh
			g4.right.mid  [mix markMiddle (markMiddle + extend) 0.5] (belowMarkBot + markHalfStroke) [heading RIGHTWARD]
			archv
			g4.up.end     (markMiddle + extend) belowMarkTop [heading UPWARD]
	
	create-glyph 'dblArchBelow' : glyph-construction
		set-width 0
		assign-unicode 0x33C
		include glyphs.dblBreveBelow
		include : FlipAround markMiddle [mix belowMarkTop belowMarkBot 0.5]
		set-anchor 'below' MARK markMiddle 0 markMiddle belowMarkBot
	
	create-glyph 'leftHalfCircleBelow' : glyph-construction
		set-width 0
		assign-unicode 0x31C
		set-anchor 'below' MARK markMiddle 0 markMiddle belowMarkBot
		include : dispiro
			g4.left.start markMiddle belowMarkTop [widths.heading markFine markFine LEFTWARD]
			archv
			g4.down.mid   (markMiddle - (belowMarkTop - belowMarkBot) / 2) [mix belowMarkTop belowMarkBot 0.5] [heading DOWNWARD]
			arcvh
			g4.right.end  markMiddle belowMarkBot [heading RIGHTWARD]
		apply-transform : Translate ((belowMarkTop - belowMarkBot) / 4) 0
	
	create-glyph 'rightHalfCircleBelow' : glyph-construction
		set-width 0
		assign-unicode 0x339
		include glyphs.leftHalfCircleBelow
		include : FlipAround markMiddle [mix belowMarkTop belowMarkBot 0.5]
		set-anchor 'below' MARK markMiddle 0 markMiddle belowMarkBot
	
	create-glyph 'leftHalfCircleAbove' : glyph-construction
		set-width 0
		assign-unicode 0x351
		set-anchor 'above' MARK markMiddle XH markMiddle aboveMarkTop
		include glyphs.rightHalfCircleBelow
		include : FlipAround markMiddle (XH / 2)
	
	create-glyph 'rightHalfCircleAbove' : glyph-construction
		set-width 0
		assign-unicode 0x357
		set-anchor 'above' MARK markMiddle XH markMiddle aboveMarkTop
		include glyphs.leftHalfCircleBelow
		include : FlipAround markMiddle (XH / 2)
	
	# Turned below marks
	create-glyph 'graveBelow' : glyph-construction
		set-width 0
		assign-unicode 0x316
		set-anchor 'below' MARK markMiddle 0 markMiddle belowMarkBot
		include glyphs.graveAbove
		include : FlipAround markMiddle (XH / 2)
	
	create-glyph 'acuteBelow' : glyph-construction
		set-width 0
		assign-unicode 0x317
		set-anchor 'below' MARK markMiddle 0 markMiddle belowMarkBot
		include glyphs.acuteAbove
		include : FlipAround markMiddle (XH / 2)
	
	create-glyph 'dotBelow' : glyph-construction
		set-width 0
		assign-unicode 0x323
		set-anchor 'below' MARK markMiddle 0 markMiddle belowMarkBot
		include glyphs.dotAbove
		include : FlipAround markMiddle (XH / 2)
	
	create-glyph 'dieresisBelow' : glyph-construction
		set-width 0
		assign-unicode 0x324
		set-anchor 'below' MARK markMiddle 0 markMiddle belowMarkBot
		include glyphs.dieresisAbove
		include : FlipAround markMiddle (XH / 2)
	
	create-glyph 'tildeBelow' : glyph-construction
		set-width 0
		assign-unicode 0x330
		set-anchor 'below' MARK markMiddle 0 markMiddle belowMarkBot
		include glyphs.tildeAbove
		include : FlipAround markMiddle (XH / 2)
	
	create-glyph 'ringBelow' : glyph-construction
		set-width 0
		assign-unicode 0x325
		set-anchor 'below' MARK markMiddle 0 markMiddle belowMarkBot
		include glyphs.ringAbove
		include : FlipAround markMiddle (XH / 2)
	
	create-glyph 'macronBelow' : glyph-construction
		set-width 0
		assign-unicode 0x331
		set-anchor 'below' MARK markMiddle 0 markMiddle belowMarkBot
		
		include glyphs.macronAbove
		include : FlipAround markMiddle (XH / 2)
	
	create-glyph 'invBridgeBelow' : glyph-construction
		set-width 0
		assign-unicode 0x33A
		set-anchor 'below' MARK markMiddle 0 markMiddle belowMarkBot
		
		include glyphs.bridgeAbove
		include : FlipAround markMiddle (XH / 2)
	
	create-glyph 'underlineBelow' : glyph-construction
		set-width 0
		assign-unicode 0x332
		set-anchor 'below' MARK markMiddle 0 markMiddle belowMarkBot
		
		include glyphs.overlineAbove
		include : FlipAround markMiddle (XH / 2)
	
	create-glyph 'dblUnderlineBelow' : glyph-construction
		set-width 0
		assign-unicode 0x333
		set-anchor 'below' MARK markMiddle 0 markMiddle belowMarkBot
		
		include glyphs.dblOverlineAbove
		include : FlipAround markMiddle (XH / 2)
	
	create-glyph 'circumflexBelow' : glyph-construction
		set-width 0
		assign-unicode 0x32D
		set-anchor 'below' MARK markMiddle 0 markMiddle belowMarkBot
		
		include glyphs.caronAbove
		include : FlipAround markMiddle (XH / 2)
	
	create-glyph 'caronBelow' : glyph-construction
		set-width 0
		assign-unicode 0x32C
		set-anchor 'below' MARK markMiddle 0 markMiddle belowMarkBot
		
		include glyphs.circumflexAbove
		include : FlipAround markMiddle (XH / 2)
	
	create-glyph 'breveBelow' : glyph-construction
		set-width 0
		assign-unicode 0x32E
		set-anchor 'below' MARK markMiddle 0 markMiddle belowMarkBot
		
		include glyphs.archAbove
		include : FlipAround markMiddle (XH / 2)
	
	create-glyph 'archBelow' : glyph-construction
		set-width 0
		assign-unicode 0x32F
		set-anchor 'below' MARK markMiddle 0 markMiddle belowMarkBot
		
		include glyphs.breveAbove
		include : FlipAround markMiddle (XH / 2)
	
	create-glyph 'commaBelow' : glyph-construction
		set-width 0
		assign-unicode 0x326
		set-anchor 'below' MARK markMiddle 0 markMiddle belowMarkBot
		
		include glyphs.turnCommaAbove
		include : FlipAround markMiddle (XH / 2)
	
	create-glyph 'barBelow' : glyph-construction
		set-width 0
		assign-unicode 0x329
		set-anchor 'below' MARK markMiddle 0 markMiddle belowMarkBot
		include glyphs.barAbove
		include : FlipAround markMiddle (XH / 2)
	
	create-glyph 'dblBarBelow' : glyph-construction
		set-width 0
		assign-unicode 0x348
		set-anchor 'below' MARK markMiddle 0 markMiddle belowMarkBot
		include glyphs.dblBarAbove
		include : FlipAround markMiddle (XH / 2)
	
	create-glyph 'crossBelow' : glyph-construction
		set-width 0
		assign-unicode 0x353
		set-anchor 'below' MARK markMiddle 0 markMiddle belowMarkBot
		
		include glyphs.crossAbove
		include : FlipAround markMiddle (XH / 2)

# horn and angle marks
symbol-block 'Corner marks'
	create-glyph 'horn' : glyph-construction
		set-width 0
		assign-unicode 0x31B
		local radius : markHalfStroke * 1.5
		include : Ring (XH + ACCENT + radius) (XH + ACCENT - radius) (markMiddle - radius) (markMiddle + radius)
		include : dispiro
			g4 (markMiddle + radius) (XH + ACCENT) [widths 0 (markFine * 1.75)]
			quadcontrols   0 0.75 5 important
			g4 (markMiddle - radius - markHalfStroke) (aboveMarkBot - radius)
		apply-transform : Upright
		apply-transform : Translate (-markMiddle + radius * 0.35) 0
		apply-transform : Italify
		set-anchor 'topright' MARK 0 (aboveMarkBot + markFine * 0.75) 0 aboveMarkTop
	
	create-glyph 'leftangleTR' : glyph-construction
		set-width 0
		assign-unicode 0x31A
		include : VBarLeft 0 aboveMarkBot aboveMarkTop (markFine * 2)
		include : HBarTop (-markExtend * 1.5) 0 aboveMarkTop (markFine * 2)
		set-anchor 'topright' MARK 0 XH 0 aboveMarkTop
	
	create-glyph 'dotTR' : glyph-construction
		set-width 0
		assign-unicode 0x358
		include : Ring (XH + ACCENT + DOTRADIUS) (XH + ACCENT - DOTRADIUS) (0 - DOTRADIUS) (0 + DOTRADIUS) true
		set-anchor 'topright' MARK 0 XH 0 aboveMarkTop
	
	create-glyph 'commaTR' : glyph-construction
		set-width 0
		assign-unicode 0x315
		include glyphs.commaAbove
		apply-transform : Upright
		apply-transform : Translate (RIGHTSB - MIDDLE - OX) (XH - aboveMarkTop - commaOvershoot)
		apply-transform : Italify
		set-anchor 'topright' MARK 0 XH 0 aboveMarkTop
	
	create-glyph 'rtailBR' : glyph-construction
		set-width 0
		assign-unicode 0x322
		
		set-anchor 'bottomright' MARK 0 0 0 belowMarkBot
		include : dispiro
			widths.rhs
			flat 0 (-O) [heading DOWNWARD]
			curl 0 0 [heading DOWNWARD]
			straight.right.end (HOOKX - HALFSTROKE * HVCONTRAST) (-HOOK + HALFSTROKE)
	
	create-glyph 'ltailBR' : glyph-construction
		set-width 0
		assign-unicode 0x321
		
		set-anchor 'bottomright' MARK 0 0 0 belowMarkBot
		include : dispiro
			widths.rhs
			flat 0 (-O) [heading DOWNWARD]
			curl 0 0 [heading DOWNWARD]
			straight.left.end (-HOOKX - HALFSTROKE * HVCONTRAST) (-HOOK - HALFSTROKE)

# Overlay Marks
symbol-block 'Overlay Marks'
	create-glyph 'tildeOver' : glyph-construction
		set-width 0
		assign-unicode 0x334
		set-anchor 'overlay' MARK markMiddle (XH / 2) markMiddle (XH / 2)
		include glyphs.tildeAbove
		apply-transform : Upright
		apply-transform : Translate 0 (XH / 2 - [mix aboveMarkTop aboveMarkBot 0.5])
		apply-transform : Italify
	
	create-glyph 'barOver' : glyph-construction
		set-width 0
		assign-unicode 0x335
		set-anchor 'overlay' MARK markMiddle (XH / 2) markMiddle (XH / 2)
		include : HOverlayBar (markMiddle - LONGJUT * 0.75) (markMiddle + LONGJUT * 0.75) (XH / 2)
	
	create-glyph 'longBarOver' : glyph-construction
		set-width 0
		assign-unicode 0x336
		set-anchor 'overlay' MARK markMiddle (XH / 2) markMiddle (XH / 2)
		include : HOverlayBar ([mix 0 SB 0.5] - WIDTH) ([mix WIDTH RIGHTSB 0.5] - WIDTH) (XH / 2)
	
	create-glyph 'shortSlashOver' : glyph-construction
		set-width 0
		assign-unicode 0xE090
		set-anchor 'overlay' MARK markMiddle (XH / 2) markMiddle (XH / 2)
		include : FlatSlashShape markMiddle (XH / 2) (0.5 * OVERLAYSTROKE)
	
	create-glyph 'slashOver' : glyph-construction
		set-width 0
		assign-unicode 0x337
		local fine : 0.375 * OVERLAYSTROKE
		set-anchor 'slash' MARK markMiddle (XH / 2) markMiddle (XH / 2)
		include : dispiro
			flat (SB - O * 3 + fine - WIDTH) (XH * (-0.1)) [widths fine fine]
			curl (RIGHTSB + O * 3 - fine - WIDTH) (XH * 1.1)
	
	create-glyph 'longSlashOver' : glyph-construction
		set-width 0
		assign-unicode 0x338
		local fine : 0.375 * OVERLAYSTROKE
		set-anchor 'slash' MARK markMiddle (XH / 2) markMiddle (XH / 2)
		include : dispiro
			flat (SB - O * 3 + fine - WIDTH) (XH * 0.5 - CAP * 0.6) [widths fine fine]
			curl (RIGHTSB + O * 3 - fine - WIDTH) (XH * 0.5 + CAP * 0.6)

# Double-character arcs
symbol-block 'Tie marks'
	create-glyph 'tieAbove' : glyph-construction
		set-width 0
		assign-unicode 0x361
		include : dispiro
			widths.rhs (markHalfStroke * 2)
			g4 [mix 0 markMiddle 1.25] aboveMarkBot
			quadcontrols  0.5 1
			g4.right.mid  0 aboveMarkTop [heading RIGHTWARD]
			quadcontrols  0.5 0
			g4 [mix 0 markMiddle (-1.25)] aboveMarkBot
		apply-transform : Upright
		apply-transform : Translate 0 (CAP - XH)
		apply-transform : Italify
	create-glyph 'turnTieAbove' : glyph-construction
		set-width 0
		assign-unicode 0x35D
		include : dispiro
			widths.lhs (markHalfStroke * 2)
			g4 [mix 0 markMiddle 1.25] aboveMarkTop
			quadcontrols  0.5 1
			g4.right.mid  0 aboveMarkBot [heading RIGHTWARD]
			quadcontrols  0.5 0
			g4 [mix 0 markMiddle (-1.25)] aboveMarkTop
		apply-transform : Upright
		apply-transform : Translate 0 (CAP - XH)
		apply-transform : Italify
	turned 'tieBelow' 0x35C 'tieAbove' 0 (XH / 2)
	
	create-glyph 'overlineTieAbove' : glyph-construction
		set-width 0
		assign-unicode 0x35E
		include : dispiro
			widths.center (markHalfStroke * 2)
			flat [mix 0 markMiddle 1.25] (XH + ACCENT)
			curl [mix 0 markMiddle (-1.25)] (XH + ACCENT)
		apply-transform : Upright
		apply-transform : Translate 0 (CAP - XH)
		apply-transform : Italify
	composite 'underlineTieBelow' glyphs.overlineTieAbove [FlipAround 0 (XH / 2)] [into-unicode 0x35F]

symbol-block 'Greek Polytonic Marks'
	define [GreekLRDoubleMark id unicode leftGlyph rightGlyph] : begin
		create-glyph id : glyph-construction
			set-width 0
			local k : -WIDTH * 0.15
			
			include leftGlyph
			apply-transform : Translate (2 * k) 0
			include rightGlyph
			apply-transform : Translate (0 - k) 0
			
			set-anchor 'above' MARK markMiddle XH markMiddle aboveMarkTop
		create-glyph ('spaced_' + id) : glyph-construction
			assign-unicode unicode
			include glyphs.space AS_BASE
			include glyphs.(id)
	
	GreekLRDoubleMark 'psilivaria' 0x1FCD glyphs.commaAbove glyphs.variaAbove
	GreekLRDoubleMark 'psilioxia'  0x1FCE glyphs.commaAbove glyphs.oxiaAbove
	GreekLRDoubleMark 'dasiavaria' 0x1FDD glyphs.revCommaAbove glyphs.variaAbove
	GreekLRDoubleMark 'dasiaoxia'  0x1FDE glyphs.revCommaAbove glyphs.oxiaAbove
	
	local yc : aboveMarkTop - [adviceBlackness 6] + DOTRADIUS * 0.3 + commaOvershoot
	
	create-glyph 'psiliperispomeni' : glyph-construction
		set-width 0
		include glyphs.commaAbove
		local radius : Math.max (markExtend - [adviceBlackness 6]) ([adviceBlackness 6] * 1.5)
		include : dispiro
			widths.lhs : Math.min (markHalfStroke * 2) ([adviceBlackness 6.5] * 1.25)
			g4.up.start (markMiddle - radius) yc [heading UPWARD]
			arcvh
			g4.right.mid markMiddle (yc + radius)
			archv
			g4.down.end (markMiddle + radius) yc [heading DOWNWARD]
		set-anchor 'above' MARK markMiddle XH markMiddle (aboveMarkTop + markHalfStroke * 2)
	
	create-glyph 'spaced_psiliperispomeni' : glyph-construction
		assign-unicode 0x1FCF
		include glyphs.space AS_BASE
		include glyphs.psiliperispomeni
	
	create-glyph 'dasiaperispomeni' : glyph-construction
		set-width 0
		include glyphs.revCommaAbove
		local radius : Math.max (markExtend - [adviceBlackness 6]) ([adviceBlackness 6] * 1.5)
		include : dispiro
			widths.lhs : Math.min (markHalfStroke * 2) ([adviceBlackness 6.5] * 1.25)
			g4.up.start (markMiddle - radius) yc [heading UPWARD]
			arcvh
			g4.right.mid markMiddle (yc + radius)
			archv
			g4.down.end (markMiddle + radius) yc [heading DOWNWARD]
		set-anchor 'above' MARK markMiddle XH markMiddle (aboveMarkTop + markHalfStroke * 2)
	
	create-glyph 'spaced_dasiaperispomeni' : glyph-construction
		assign-unicode 0x1FDF
		include glyphs.space AS_BASE
		include glyphs.dasiaperispomeni